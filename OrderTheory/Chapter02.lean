/-
Copyright (c) 2024 Paul D. Rowe. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul D. Rowe
-/
import OrderTheory.Exercises01
import Mathlib.Order.Sublattice
import Mathlib.Order.Ideal
import Mathlib.Data.Set.Card
import Mathlib.Order.Height
import Mathlib.Order.RelClasses

open scoped Classical

variable {P Q Î¹ X L K : Type}
/-!
  # Chapter 2: Lattices and Complete Lattices

  We continue with notes on the text and the translation of the various concepts
  into Lean.
-/

/-!
  # Lattices as ordered sets

  This section is all about lattices which are essentially partial orders with
  some additional structure related to the existence of upper and lower bounds.
-/

/-!
  ## 2.1 Defintions

  Let `P` be a partial order and let `S âŠ† P` be a subset of `P`. An element
  `x âˆˆ P` is an upper bound of `S` if and only if for all `s âˆˆ S`, `s â‰¤ x`. A
  lower bound is natrually defined dually.

  I can't find a separate predicate in Mathlib to say `x` is an upper bound
  of a set `S`. But it does contain notation for the set of all upper (and lower)
  bounds of a set. These are denoted `upperBounds` and `lowerBound` respectively.
  They each consume a set `S` and give back the set `{x | âˆ€ s âˆˆ S, s â‰¤ x}` (or dually
  for lower bounds). The text denotes these by `Sáµ˜` and `SË¡` respectively. I will
  introduce that notation for Lean, just to keep consistent.
-/

postfix:max "áµ˜" => upperBounds
postfix:max "Ë¡" => lowerBounds

/-!
  Since `â‰¤` is transitive, `Sáµ˜` is always an upper set, and `SË¡` always a lower set.
-/

theorem isUpperSet_upperBounds [PartialOrder P] (S : Set P) : IsUpperSet Sáµ˜ :=
  Î» _ _ le mem _ smem â†¦ (mem smem).trans le

theorem isLowerSet_lowerBounds [PartialOrder P] (S : Set P) : IsLowerSet SË¡ :=
  Î» _ _ le mem _ smem â†¦ le.trans (mem smem)

/-!
  If `Sáµ˜` has a least element `x`, it is called the least upper bound of S. Mathlib
  expresses this as `IsLUB S x`. In fact, `IsLUB` is defined just as in the text, namely
  as the least element of upperBounds. The following formulation is equivalent:

  (i) `x` is an upper bound of `S` and
  (ii) for all upper bounds `y` of `S`, `x â‰¤ y`
-/

/-- This seems to be true essentially by definition in Mathlib -/
theorem IsLUB_iff [PartialOrder P] (S : Set P) {x : P} :
    IsLUB S x â†” x âˆˆ Sáµ˜ âˆ§ âˆ€ y âˆˆ Sáµ˜, x â‰¤ y := by exact Eq.to_iff rfl

/-!
  The least upper bound of `S` exists if and only if there exists `x : P` such that
  `âˆ€ y : P, ((âˆ€ s âˆˆ S, s â‰¤ y) â†” x â‰¤ y)`
-/

theorem exists_LUB_iff [PartialOrder P] (S : Set P) :
    (âˆƒ x, IsLUB S x) â†” âˆƒ x, âˆ€ y : P, ((âˆ€ s âˆˆ S, s â‰¤ y) â†” x â‰¤ y) := by
  constructor
  Â· intro âŸ¨x, lubâŸ©
    use x
    intro y
    constructor
    Â· exact Î» h â†¦ lub.2 h
    Â· exact Î» le _ mem â†¦ (lub.1 mem).trans le
  Â· intro âŸ¨x, hâŸ©
    use x
    constructor
    Â· intro s mem
      specialize h x
      simp only [le_refl, iff_true] at h
      exact h s mem
    Â· exact Î» y mem â†¦ (h y).mp mem

/-!
  Of course the greatest lower bound works dually. It is denoted `IsGLB S x` in Mathlib.

  The text uses the notation `â‹S` for the least upper bound of a set (\bigvee) and
  `â‹€S` (\bigwedge) for the greatest lower bound when these exist. Mathlib uses `sSup` and
  `sInf` (set supremum and infimum) for these.
-/

/-!
  ## 2.2 Top and bottom

  It is easily seen that if `P` has a top element, then `Páµ˜ = {âŠ¤}` in which case
  `sSup P = âŠ¤`. When `P` has no top element `Páµ˜ = âˆ…` so `sSup P` does not exist.
-/

lemma example_2_2a [PartialOrder P] [OrderTop P] :
    (Set.univ : Set P)áµ˜ = {âŠ¤} := by simp

lemma example_2_2b [SemilatticeSup P] [OrderTop P] :
    IsLUB (Set.univ : Set P) âŠ¤ := by exact âŸ¨by simp, by simpâŸ©

lemma example_2_2c [PartialOrder P] :
    (âˆ… : Set P)áµ˜ = Set.univ := by simp

lemma example_2_2d [PartialOrder P] : (âˆƒ x, IsLUB (âˆ… : Set P) x) â†” âˆƒ b : P, âˆ€ p, b â‰¤ p := by
  constructor
  Â· intro âŸ¨b, hbâŸ©
    use b
    intro p
    exact hb.2 (by simp)
  Â· intro âŸ¨b, hbâŸ©
    use b
    constructor
    Â· simp
    Â· intro p _
      exact hb p

/-!
  ## 2.3 Notation

  For Mathlib, we can write `x âŠ” y` for the least upper bound of `x` and `y`
  as long as we have an instance `[SemilatticSup P]`. (We can actually write it
  as long as we have `[Sup P]` but that type class is for notation only and does
  not enforce the properties of least upper bounds.) Similarly, `x âŠ“ y` is the
  greatest lower bound, or infimum of `x` and `y` as long as we have `[SemilatticInf P]`.

  As above, we must write `sSup S` and `sInf S` for the supremum and infimum of
  a set `S`. But again, we need an instance of `[SupSet P]` or `[InfSet P]` for those
  to even make sense. And if we want them to have the properties of supremum and
  infimum, we must have an instance `[CompleteSemilatticeSup P]` or
  `[CompleteSemilatticInf P]`.

  Finally, if we have an indexed family of elements of p, `{páµ¢ : P | i âˆˆ Î¹}`,
  then this manifests in Mathlib as follows. The existence of this family is denoted
  `p : Î¹ â†’ P`, and we denote the supremum of the family by `â¨† i, p i`. As in all the
  cases above, this notation is unlocked by a typeclass, in this case `[SupSet P]`,
  and it only has the properties we want when we have an instance
  `[CompleteSemilatticeSup P]`. The corresponding dual notation is `â¨… i, p i` with
  all the expected caveats about type classes.

  Essentially, we can only really access the notations for sups and infs of pairs
  or sets if we have notation instances that tell us how such functions are defined. And they
  only have the meaning we want when we have instances of the relevant type classes.
  To put it another way, unless sup and inf are defined for all pairs (respectively
  all sets) then we should not be using the notation. This differs somewhat from the
  text which does use the notation `x âŠ” y` even if it only exists for that one pair.
-/

/-!
  ## 2.4 Definitions

  When both `x âŠ” y` and `x âŠ“ y` exists for all pairs `x` and `y` in an partial order `P`
  then we call `P` a lattice. This is a type class in Mathlib: `[Lattice P]`. The text doesn't
  mention at this point structures for which only one or the other of `âŠ”` or `âŠ“` is defined
  for all pairs, but Mathlib has this built in. If `âŠ”` is defined for all pairs then
  we have an instance `[SemilatticeSup P]`. If it's `âŠ“` that is defined for all pairs
  then we use `[SemilatticeInf P]`.

  If the supremum and infimum exist for all subsets `S : P`, (i.e. `sSup S` and `sInf S`
  exist and have the properties of supremum and infimum) then `P` is called a complete
  lattice. This is written `[CompleteLattice P]` in Mathlib. The one-sided versions in which
  only sups or infs of all sets are defined are written `[CompleteSemilatticeSup P]` and
  `[CompleteSemilatticeInf P]`. The Mathlib definitions for these one-sided version state
  that they are rarely used because any one-sided version is actually a complete lattice.
  I expect this will be discussed in the text.
-/

/-!
  ## 2.5 Remarks on âŠ” and âŠ“

  (1) If `(x :P) â‰¤ y` then `{x, y}áµ˜ = â†‘áµ–y` and `{x, y}Ë¡ = â†“áµ–x`. It follows that
      `x âŠ” y = y` and `x âŠ“ y = x`. Setting `x = y` we see that `x âŠ” x = x` and
      `x âŠ“ x = x`.
-/

lemma example_2_5_1a [PartialOrder P] {x y : P} (h : x â‰¤ y) : {x, y}áµ˜ = â†‘áµ–y âˆ§ {x, y}Ë¡ = â†“áµ–x := by
  constructor
  Â· ext p
    constructor <;> intro mem
    Â· simp_all
    Â· simp_all; exact h.trans mem
  Â· ext p
    constructor <;> intro mem
    Â· simp_all
    Â· simp_all; exact mem.trans h

lemma example_2_5_1b [Lattice P] {x y : P} (h : x â‰¤ y) : x âŠ” y = y âˆ§ x âŠ“ y = x := by
  -- constructor
  -- Â· exact sup_of_le_right h
  -- Â· exact inf_of_le_left h
  simp_all only [ge_iff_le, sup_of_le_right, inf_of_le_left, and_self]

/-!
  (2) Nothing to formalize here. It's just noting that the `x âŠ” y` can fail to
      exist either because `{x, y}áµ˜` is empty or because it has no least element.

  (3) Nothing to formalize here either.

  (4) If `P` is a lattice, then for all `a`, `b`, `c`, `d`,

    (i) `a â‰¤ b` implies `a âŠ” c â‰¤ b âŠ” c` and `a âŠ“ c â‰¤ b âŠ“ c`

    (ii) `a â‰¤ b` and `c â‰¤ d` imply `a âŠ” c â‰¤ b âŠ” d` and `a âŠ“ c â‰¤ b âŠ“ d`.

    These must already be in Mathlib.
-/

lemma example_2_5_4i [Lattice P] {a b : P} (c : P) (hab : a â‰¤ b) :
    a âŠ” c â‰¤ b âŠ” c âˆ§ a âŠ“ c â‰¤ b âŠ“ c := by
  constructor
  Â· --exact sup_le_sup_right hab c
    have lea : a â‰¤ b âŠ” c := hab.trans le_sup_left
    have lec : c â‰¤ b âŠ” c := le_sup_right
    apply sup_le lea lec
  Â· --exact inf_le_inf_right c hab
    have lea : a âŠ“ c â‰¤ b := inf_le_left.trans hab
    have lec : a âŠ“ c â‰¤ c := inf_le_right
    apply le_inf lea lec

lemma example_2_5_4ii [Lattice P] {a b c d : P} (hab : a â‰¤ b) (hcd : c â‰¤ d) :
    a âŠ” c â‰¤ b âŠ” d âˆ§ a âŠ“ c â‰¤ b âŠ“ d := by
  constructor
  Â· --exact sup_le_sup hab hcd
    have lea : a â‰¤ b âŠ” d := hab.trans le_sup_left
    have lec : c â‰¤ b âŠ” d := hcd.trans le_sup_right
    exact sup_le lea lec
  Â· --exact inf_le_inf hab hcd
    exact le_inf (inf_le_left.trans hab) (inf_le_right.trans hcd)

lemma example_2_5_5 [Lattice P] {a b c : P} (hba : b â‰¤ a) (habc : a â‰¤ b âŠ” c) :
    a âŠ” c = b âŠ” c := by -- Follow proof in text
  have hbcc : c âŠ” (b âŠ” c) = b âŠ” c := by apply (example_2_5_1b (by simp)).1
  have h2 : b âŠ” c â‰¤ a âŠ” c := by apply (example_2_5_4i c hba).1
  have h3 : a âŠ” c â‰¤ c âŠ” (b âŠ” c) := by simp_all
  rw [hbcc] at h3
  exact le_le_iff_eq.mp âŸ¨h3, h2âŸ©

/-!
  ## 2.6 Examples

  (1) Every chain is a lattice. This is known to Mathlib as LinearOrder.toLattice.
      This has a bunch of observations about â„, â„š, â„¤, â„• that follow quite naturally
      and are quite intuitive.

  (2) For any type `X` the type of subsets of `X`, `Set X` is complete lattice under
      set inclusion. Mathlib seems to know this by I can't find where it is defined.

-/

lemma example_2_6_2a {A : Î¹ â†’ Set X} : â¨† i, A i = â‹ƒ i, A i := rfl

lemma example_2_6_2b {A : Î¹ â†’ Set X} : â¨… i, A i = â‹‚ i, A i := rfl

/-!
  (3) If `ğ” : Set (Set X)`, then `ğ”` is called a lattice of sets if it is
      closed under finite unions and intersections, and complete lattice if
      it is closed under arbitrary unions and intersections.
-/

def example_2_6_3a {X : Type} (ğ” : Set (Set X))
    (hUnion : âˆ€ S T, S âˆˆ ğ” â†’ T âˆˆ ğ” â†’ S âˆª T âˆˆ ğ”)
    (hInter : âˆ€ S T, S âˆˆ ğ” â†’ T âˆˆ ğ” â†’ S âˆ© T âˆˆ ğ”) :
    Lattice { S : Set X | S âˆˆ ğ”} :=
  {
    sup := Î» S T â†¦ âŸ¨S âˆª T, hUnion S.val T.val S.property T.propertyâŸ©
    le_sup_left := by simp
    le_sup_right := by simp
    sup_le := by simp_all
    inf := Î» S T â†¦ âŸ¨S âˆ© T, hInter S.val T.val S.property T.propertyâŸ©
    inf_le_left := by simp
    inf_le_right := by simp
    le_inf := by simp_all
  }

local instance example_2_6_3bSupSet (ğ” : Set (Set X))
    (hUnion : âˆ€ (I : Set (Set X)), I âŠ† ğ” â†’ â‹ƒ i : I, i âˆˆ ğ”) :
    SupSet { S // S âˆˆ ğ”} := âŸ¨Î» I â†¦ âŸ¨â‹ƒ i : I, i, by specialize hUnion I; simp_allâŸ©âŸ©

local instance example_2_6_3bSup (ğ” : Set (Set X))
    [SupSet { S : Set X // S âˆˆ ğ”}] :
  Max { S : Set X // S âˆˆ ğ”} := âŸ¨Î» S T â†¦ sSup {S, T}âŸ©

local instance example_2_6_3InfSet {X : Type} (ğ” : Set (Set X))
    (hInter : âˆ€ (I : Set (Set X)), I âŠ† ğ” â†’ â‹‚ i : I, i âˆˆ ğ”) :
    InfSet { S : Set X // S âˆˆ ğ”} := âŸ¨Î» I â†¦ âŸ¨â‹‚ i : I, i, by specialize hInter I; simp_allâŸ©âŸ©

local instance example_2_6_3bInf (ğ” : Set (Set X))
    [InfSet { S : Set X // S âˆˆ ğ”}] :
  Min { S : Set X // S âˆˆ ğ”} := âŸ¨Î» S T â†¦ sInf {S, T}âŸ©



/-- We only need to prove it's a complete semilattice with sup -/
def example_2_6_3b {X : Type} (ğ” : Set (Set X))
    (hUnion : âˆ€ (I : Set (Set X)), I âŠ† ğ” â†’ â‹ƒ i : I, i âˆˆ ğ”)
    (_ : âˆ€ (I : Set (Set X)), I âŠ† ğ” â†’ â‹‚ i : I, i âˆˆ ğ”) :
    CompleteSemilatticeSup { S : Set X // S âˆˆ ğ”} :=
  {
    sSup := Î» I â†¦ âŸ¨â‹ƒ i : I, i, by specialize hUnion I; simp_allâŸ©
    le_sSup := by
      intro s a mema x memx
      simp_all
      use â†‘a
      refine' âŸ¨_, memxâŸ©
      use a.property
    sSup_le := by
      intro s a b x memx
      simp_all
      obtain âŸ¨i, âŸ¨âŸ¨x', memiâŸ©, memx'âŸ©âŸ© := memx
      specialize b _ _ memi
      exact b memx'
  }

lemma example_2_6_3c [PartialOrder P]
    (A : Î¹ â†’ Set P) (h : âˆ€ i, IsLowerSet (A i)) :
    IsLowerSet (â‹ƒ i, A i) âˆ§ IsLowerSet (â‹‚ i, A i) := by
  constructor
  Â· intro a b le mem
    obtain âŸ¨Ai, hAi1, hAi2âŸ© := mem
    use Ai, hAi1
    obtain âŸ¨i, hiâŸ© := hAi1
    subst Ai
    exact h i le hAi2
  Â· intro a b le mem Ai hAi
    specialize mem Ai hAi
    obtain âŸ¨i, hiâŸ© := hAi
    subst Ai
    exact h i le mem

@[simp]
local instance instOrderTop {n : â„•} : OrderTop (WithTop (WithBot (Fin' n))) :=
  {
    top := âŠ¤
    le_top := by simp
  }

@[simp]
local instance instOrderBot {n : â„•} : OrderBot (WithTop (WithBot (Fin' n))) :=
  {
    bot := â†‘âŠ¥
    bot_le := by
      intro a
      cases a with
      | top => simp
      | coe a => exact bot_le
  }

@[simp]
noncomputable
instance instrSup {n : â„•} : Max (WithTop (WithBot (Fin' n))) :=
  {
    max := Î»
      | âŠ¥, y => y
      | x, âŠ¥ => x
      | x, y => if x = y then x else âŠ¤
  }

@[simp]
noncomputable
local instance instInf {n : â„•} : Min (WithTop (WithBot (Fin' n))) :=
  {
    min := Î»
      | x, âŠ¤ => x
      | âŠ¤, y => y
      | x, y => if x = y then x else âŠ¥
  }

noncomputable
local instance instrSemilatticeSup {n : Nat} :
    SemilatticeSup (WithTop (WithBot (Fin' n))) :=
  {
    sup := instrSup.max
    le_sup_left := by
      intro x y
      cases_type* WithTop WithBot
      all_goals simp_all
      split_ifs <;> simp_all
    le_sup_right := by
      intro x y
      cases_type* WithTop WithBot
      all_goals simp_all
      split_ifs <;> simp_all
    sup_le := by
      intro x y z le1 le2
      cases_type* WithTop <;> simp_all [WithTop.none_eq_top, WithTop.some_eq_coe]
      cases_type* WithBot <;> simp_all [WithBot.none_eq_bot]
      Â· rw [WithTop.some_eq_coe]; simp
      Â· apply bot_le
      Â· rw [WithTop.some_eq_coe]; simp; assumption
      Â· rw [WithTop.some_eq_coe]; simp; assumption
      Â· split_ifs with h
        Â· rw [â†h, WithTop.some_eq_coe, WithTop.coe_le_coe]
          simp [WithBot.some_eq_coe]; assumption
        Â· rw [Fin'.le_iff] at le1 le2
          rw [le1, le2] at h
          contradiction
  }

noncomputable
local instance instSemilatticeInf {n : Nat} : Lattice (WithTop (WithBot (Fin' n))) :=
  {
    inf := instInf.min
    inf_le_left := by
      intro x y
      cases_type* WithTop WithBot
      all_goals simp_all
      all_goals split_ifs <;> simp_all
    inf_le_right := by
      intro x y
      cases_type* WithTop WithBot
      all_goals simp_all
      all_goals split_ifs <;> simp_all
    le_inf := by
      intro x y z le1 le2
      cases_type* WithTop <;> simp_all [WithTop.none_eq_top, WithTop.some_eq_coe]
      cases_type* WithBot <;> simp_all [WithBot.none_eq_bot, WithBot.some_eq_coe]
      rw [Fin'.le_iff] at le1 le2
      split_ifs with h
      Â· rw [le1, h]
      Â· rw [â†le1, le2] at h
        contradiction
  }

/-!
  ## 2.7 Lattices of subgroups

  If `G` is a group and âŸ¨SubG, âŠ†âŸ© is its ordered set of subgroups, then let
  `H`, `K` be subgroups of `G`. It's certainly true that `H âˆ© K` is also a
  subgroup of `G`, so we can define `H âŠ“ K` to be the intersection. But
  `H âˆª K` is not typically a subgroup. Nevertheless, the subgroup generated
  by `H âˆª K` *is* a subgroup and is actually `H âŠ” K`. Sadly there is no simple
  general formula for this as a set. Normal subgroups are better behaved. For
  normal subgroups, the set `HK = { h*k | h âˆˆ H âˆ§ k âˆˆ K }` is normal and
  serves as the supremum of `H` and `K` in the set of normal subgroups of `G`.

  Mathlib does not seem to have any declaration of the type of normal subgroups
  of a group `G`. After a little effort trying to make one, I realized it's
  not trivial. It could be a good exercise, but is not the point of the
  current project, so I will defer it until later or until somebody else
  can do it instead.
-/

/-!
  # Lattices as algebraic structures

  We begin to look at lattices as algebraic structures where the operators
  `âŠ”` and `âŠ“` are primary. We explore how these are connected with `â‰¤`.
-/

/-!
  ## 2.8 The Connecting Lemma

  Let `L` be a lattice and let `a b : L`. Then the following are equivalent:

  (i) `a â‰¤ b`

  (ii) `a âŠ” b = b`

  (iii) `a âŠ“ b = a`
-/

theorem example_2_8_sup_eq_iff_le [Lattice L] {a b : L} : a âŠ” b = b â†” a â‰¤ b :=
  âŸ¨by rw [sup_eq_right]; tauto, Î» le â†¦ (example_2_5_1b le).1âŸ©

theorem example_2_8_inf_eq_iff_le [Lattice L] {a b : L} : a âŠ“ b = a â†” a â‰¤ b :=
  âŸ¨by rw [inf_eq_left]; tauto, Î» le â†¦ (example_2_5_1b le).2âŸ©

theorem example2_8_sup_eq_iff_inf_eq [Lattice L] {a b : L} : a âŠ” b = b â†” a âŠ“ b = a := by
  rw [example_2_8_inf_eq_iff_le, example_2_8_sup_eq_iff_le]

/-!
  ## 2.9 Theorem

  Let `L` be a lattice. Then for all `a b c : L`, `âŠ”` and `âŠ“` satisfy:

  (L1) `(a âŠ” b) âŠ” c = a âŠ” (b âŠ” c)` (sup is associative)
  (L1') `(a âŠ“ b) âŠ“ c = a âŠ“ (b âŠ“ c)` (inf is associative)
  (L2) `a âŠ” b = b âŠ” a` (sup is commutative)
  (L2') `a âŠ“ b = b âŠ“ a` (inf is commutative)
  (L3) `a âŠ” a = a` (sup is idempotent)
  (L3') `a âŠ“ a = a` (inf is idempotent)
  (L4) `a âŠ” (a âŠ“ b) = a` (absorption for inf then sup)
  (L4') `a âŠ“ (a âŠ” b) = a` (absorption for sup then inf)
-/

/-- This is known in Mathlib as `sup_assoc`. The Mathlib proof is recreated here. -/
theorem example_2_9_sup_assoc [Lattice L] {a b c : L} : (a âŠ” b) âŠ” c = a âŠ” (b âŠ” c) :=
  eq_of_forall_ge_iff (Î» x â†¦ by simp only [sup_le_iff]; rw [and_assoc])

/-- This is known in Mathlib as `inf_assoc`. This is provable as the dual of the above. -/
theorem example_2_9_inf_assoc [Lattice L] {a b c : L} : (a âŠ“ b) âŠ“ c = a âŠ“ (b âŠ“ c) :=
  @example_2_9_sup_assoc Láµ’áµˆ _ _ _ _

/-- This is known in Mathlib as `sup_comm`. -/
theorem example_2_9_sup_comm [Lattice L] {a b : L} : a âŠ” b = b âŠ” a :=
  eq_of_forall_ge_iff (Î» x â†¦ by simp only [sup_le_iff]; rw [and_comm])

/-- This is known in Mathlib as `inf_comm`. -/
theorem example_2_9_inf_comm [Lattice L] {a b : L} : a âŠ“ b = b âŠ“ a :=
  eq_of_forall_le_iff (Î» x â†¦ by simp only [le_inf_iff]; rw [and_comm])

/-- This is known in Mathlib as `sup_idem` and proved by simp. -/
theorem example_2_9_sup_idem [Lattice L] {a : L} : a âŠ” a = a := by simp

/-- This is known in Mathlib as `inf_idem` and proved by simp. -/
theorem example_2_9_inf_idem [Lattice L] {a : L} : a âŠ“ a = a := by simp

/-- This is known in Mathlib as `sup_inf_self`. -/
theorem example_2_9_sup_inf_self [Lattice L] {a b : L} : a âŠ” (a âŠ“ b) = a := by
  rw [sup_comm, example_2_8_sup_eq_iff_le]
  exact inf_le_left

/-- This is known in Mathlib as `inf_sup_self`. -/
theorem example_2_9_inf_sup_self [Lattice L] {a b : L} : a âŠ“ (a âŠ” b) = a :=
  @example_2_9_sup_inf_self Láµ’áµˆ _ _ _

/-!
  Let `L` be a set (type) equipped with two operations `âŠ”` and `âŠ“`
  that satisfy (L1)-(L4) and (L1')-(L4') above. Then

  (i) For all `a b : L`, we have `a âŠ” b = b` if and only if `a âŠ“ b = a`

  (ii) Define `â‰¤` on `L` by `a â‰¤ b` if and only if `a âŠ” b = b`. Then `L` is a
  partial order under `â‰¤`.

  (iii) With `â‰¤` as above, `L` is actually a lattice in which `âŠ”` is sup and
  `âŠ“` is inf.

  This is essentially `Lattice.mk'` which does not even use `L3` and `L3'`. I
  will try to get by without either of them.
-/

theorem example_2_10_i [Max L] [Min L]
    (L2 : âˆ€ {a b : L}, a âŠ” b = b âŠ” a)
    (L2': âˆ€ {a b : L}, a âŠ“ b = b âŠ“ a)
    (L4 : âˆ€ {a b : L}, a âŠ” (a âŠ“ b) = a)
    (L4': âˆ€ {a b : L}, a âŠ“ (a âŠ” b) = a) {a b : L} :
    a âŠ” b = b â†” a âŠ“ b = a := by
  constructor <;> intro h
  Â· rw [â†h]; exact L4'
  Â· rw [â†h, L2, L2']; exact L4

def LatPO [Max L] [Min L]
    (L1 : âˆ€ {a b c : L}, (a âŠ” b) âŠ” c = a âŠ” (b âŠ” c))
    (L1': âˆ€ {a b c : L}, (a âŠ“ b) âŠ“ c = a âŠ“ (b âŠ“ c))
    (L2 : âˆ€ {a b : L}, a âŠ” b = b âŠ” a)
    (L2': âˆ€ {a b : L}, a âŠ“ b = b âŠ“ a)
    (L4 : âˆ€ {a b : L}, a âŠ” (a âŠ“ b) = a)
    (L4': âˆ€ {a b : L}, a âŠ“ (a âŠ” b) = a) : Lattice L :=
  have L3 : âˆ€ (a : L), a âŠ” a = a := Î» a â†¦
    calc
      a âŠ” a = a âŠ” a âŠ“ (a âŠ” a) := by rw [L4']
      _ = a := by rw [L4]
  {
    sup := Max.max
    inf := Min.min
    le := Î» a b â†¦ a âŠ” b = b
    le_refl := L3
    le_trans := by
      intro a b c le1 le2
      simp at le1 le2 âŠ¢
      rw [â†le2, â†L1, le1]
    le_antisymm := by
      intro a b le1 le2
      simp at le1 le2
      rw [â†le1]; nth_rewrite 1 [â†le2]; rw [L2]
    le_sup_left := by
      intro a b; simp
      rw [â†L1, L3]
    le_sup_right := by
      intro a b; simp
      rw [L2, L1, L3]
    sup_le := by
      intro a b c le1 le2
      simp at le1 le2 âŠ¢
      rw [L1, le2, le1]
    inf_le_left := by
      intro a b; simp
      rw [L2, L4]
    inf_le_right := by
      intro a b; simp
      rw [L2, L2', L4]
    le_inf := by
      intro a b c le1 le2
      simp at le1 le2 âŠ¢
      rw [example_2_10_i L2 L2' L4 L4'] at le1 le2 âŠ¢
      rw [â†L1', le1, le2]
  }

/-!
  ## 2.11 Stocktaking

  So Lattices can be completely characterized by `âŠ”` and `âŠ“`. In all Lattices,
  the sup and inf are related to `â‰¤` exactly as above. So, we can take the
  algebraic properties as simply given without having to think about LUBs and GLBs
  if that happens to be a more convenient way of proceeding.

  Observe that associativity of `âŠ”` means that `sSup {aâ‚, ..., aâ‚™} = aâ‚ âŠ” ... âŠ” aâ‚™` (by
  a simple inductive argument). As a consequence, suprema exist for any finite set of
  elements of a lattice. The inductive argument only applies to finite sets, and of
  course the suprema of infinite subsets of a lattice need not exist in general. So,
  in general, there need not be an instance of `SupSet L` if we have `Lattice L`.
-/

/-!
  ## 2.12 Definitions

  Let `L` be a lattice. It could happen that it has both a `âŠ¤` and `âŠ¥`. We want to
  explore how to think of these from the algebraic point of view instead of the
  order-theoretic point of view.

  `L` has a "one" if there is some element `1 : L` such that for all `a : L`,
  `a âŠ“ 1 = a`. Similarly, `L` has a "zero" if there is some element `0 : L`
  such that `a âŠ” 0 = a` for all `a : L`. Then it follows that `L` has an
  algebraic `1` if and only if `L` has an order-theoretic `âŠ¤`, and dually for
  `0` and `âŠ¥`.

  A lattice with a `0` and `1` is called bounded. There does not seem to be a single class for
  bounded lattices in Mathlib. Rather, there is `BoundedOrder L` which is defined
  to be the conjunction of `OrderTop L` and `OrderBot L`. So, to say that `L` is
  a bounded lattice we would have to write `[BoundedOrder L] [Lattice L]`.

  Note that all finite lattices are automatically bounded with `sSup L = 1` and
  `sInf L = 0`. In fact, Mathlib does not seem to have an instance of `SupSet` for
  finite types with suprema. Can I just make one here? In the spirit of not
  worrying about finite sets in this pass, I will leave it alone.

  Recalling 2.6(5), note that `âŸ¨â„•, lcm, gcdâŸ©` is bounded with `1 = 0` and `0 = 1`.
-/

section

def LNat : Type := â„•

@[simp]
instance LNat.instSuplocal : Max LNat := { max := Nat.lcm }

@[simp]
instance LNat.instInflocal : Min LNat := { min := Nat.gcd }

instance LNat.instCCMWZ : CancelCommMonoidWithZero LNat := Nat.instCancelCommMonoidWithZero
instance LNat.instNGCDM : NormalizedGCDMonoid LNat := by
  haveI I : NormalizedGCDMonoid Nat := by infer_instance
  exact I

lemma gcd_lcm_self (n m : LNat) : Nat.gcd n (Nat.lcm n m) = n := by
  exact Nat.gcd_eq_left (Nat.dvd_lcm_left n m)

lemma lcm_gcd_self (n m : LNat) : Nat.lcm n (Nat.gcd n m) = n  := by
  have h := Nat.gcd_dvd_left n m
  have : normalize n = n := by simp only [normalize, normUnit, Units.val_one, mul_one,
    MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk]
  exact (lcm_eq_left_iff n _ this).mpr h

instance example_2_12_L : Lattice LNat :=
  Lattice.mk' lcm_comm lcm_assoc gcd_comm gcd_assoc lcm_gcd_self gcd_lcm_self

def example_2_12_BO : BoundedOrder LNat :=
  {
    top := 0
    bot := 1
    le_top := Î» a â†¦ Nat.lcm_zero_right a
    bot_le := Î» a â†¦ by
      change 1 âŠ” a = a
      rw [example_2_10_i (by simp [Nat.lcm_comm]) (by simp [Nat.gcd_comm]) (by simp [lcm_gcd_self]) (by simp [gcd_lcm_self])]
      exact Nat.gcd_one_left a
  }

end section

/-!
  # Sublattices, products and homomorphisms

  We now show how to derive more lattices from existing lattices
-/

/-!
  ## 2.13 Sulattices

  Let `L` be a lattice and `âˆ… â‰  M âŠ† L`. Then `M` is a sublattice of `L`
  if and only if

  `a,b âˆˆ M` implies `a âŠ” b âˆˆ M` and `a âŠ“ b âˆˆ M`.

  This manifests in Mathlib as `Sublattice (M : Set L)` that has the
  `carrier` set of elements of `M`, `supClosed'`, a proof that sups
  of elements of `M` are also in `M`, and its dual `infClosed'`.
-/

/-!
  ## 2.14 Examples

  (1) Any one-element subset of a lattice is a sublattice.
-/

def example_2_14a [Lattice L] (x : L) : Sublattice L :=
  {
    carrier := {x}
    supClosed' := by
      intro a mema b memb
      simp at mema memb
      subst a; subst b
      simp
    infClosed' := by
      intro a mema b memb
      simp at mema memb
      subst a; subst b
      simp
  }

/-!
  More generally, any non-empty chain in a lattice is a sublattice.
-/

def example_2_14b [Lattice L] {K : Set L} (h : IsChainLE K) :
    Sublattice L :=
  {
    carrier := K
    supClosed' := by
      intro a mema b memb
      by_cases eq : a = b
      Â· subst a; simp; exact memb
      Â· specialize h mema memb eq
        cases h with
        | inl h => simp_all
        | inr h => simp_all
    infClosed' := by
      intro a mema b memb
      by_cases eq : a = b
      Â· subst a; simp; exact memb
      Â· specialize h mema memb eq
        cases h with
        | inl h => simp_all
        | inr h => simp_all
  }

/-!
  (2) More diagram-based stuff

  (3) A subset `M` of a lattice `L` may be a lattice in its own right
  without being a sublattice of `L`. This is shown in one of the diagrams.
-/

/-!
  ## 2.15 Products

  Let `L` and `K` be lattices. Define `âŠ”` and `âŠ“` coordinate-wise on
  `L Ã— K`, as follows:

    (lâ‚, kâ‚) âŠ” (lâ‚‚, kâ‚‚) = (lâ‚ âŠ” lâ‚‚, kâ‚ âŠ” kâ‚‚)

    (lâ‚, kâ‚) âŠ“ (lâ‚‚, kâ‚‚) = (lâ‚ âŠ“ lâ‚‚, kâ‚ âŠ“ kâ‚‚)

  One can check that the algebraic properties of `âŠ”` and `âŠ“` are satisfied
  by these definitions, so the product also forms a lattice. This
  is the default lattice instance on `L Ã— K` in Mathlib assuming `L` and `K`
  are both lattices.

  It is routine to show that `L Ã— K` always contains sublattices isomorphic
  to `L` and to `K` (assuming they're nonempty), by taking `{lâ‚€} Ã— K` and
  `L Ã— {kâ‚€}` for any `lâ‚€` and `kâ‚€`. I'll only do one of these.
-/

def exercise_2_15_a [Lattice L] [Lattice K] (lâ‚€  : L) :
    K â‰ƒo ({ (l, _) | l = lâ‚€ } : Set (L Ã— K)) :=
  {
    toFun := Î» k â†¦ âŸ¨(lâ‚€, k), by simpâŸ©
    invFun := Î» lk â†¦ lk.val.2
    left_inv := by intro k; simp
    right_inv := by
      intro âŸ¨(l, k), prfâŸ©; simp at prf âŠ¢
      symm; exact prf
    map_rel_iff' := by
      intro k1 k2
      constructor <;> intro le
      Â· exact le.2
      Â· exact âŸ¨by rfl, leâŸ©
  }

/-!
  ## 2.16 Homomorphisms

  Lattice homomorphisms are maps that preserve sups and infs. It will be
  important to understand how these maps relate to order preserving maps
  on the lattices viewed simply as partial orders. But first we have
  to get lattice homomorphisms defined.

  In Mathlib, a `SupHom Î± Î²` requires both `Î±` and `Î²` to have `âŠ”` defined.
  It then consists of a function `f : Î± â†’ Î²` and a proof that it preserves sups,
  namely `f (aâ‚ âŠ” aâ‚‚) = f aâ‚ âŠ” f aâ‚‚`. Then a lattice homomorphism from
  `Î±` to `Î²` is a SupHom that also preserves inf. Thus `LatticeHom Î± Î²`
  comes equipped with a function, and two proofs that it preserves sups and
  infs. Those proofs are called `map_sup'` and `map_inf'`.

  The text defines a Lattice Isomorphism as a bijective Lattice Homomorphism.
  There is no `LatticeIso Î± Î²` defined in Mathlib, so it will probably turn
  out that an `OrderIso Î± Î²` between lattices `Î±` and `Î²` must be a bijective
  lattice homomorphism, but we'll see! Similarly, the text defines a
  lattice embedding to be a lattice hom that is injective. No such definition
  exists in Mathlib, so we'll have to see why.
-/

/-!
  ## 2.17 Remarks

  (1) It is easy to check that the inverse of a lattice isomorphism is a
  lattice hom, and hence also a lattice isomorphism.
-/

def example_2_17_1 [Lattice L] [Lattice K] (Î· : LatticeHom L K) (invFun : K â†’ L)
    (leftInv : Function.LeftInverse invFun Î·.toFun)
    (rightInv : Function.RightInverse invFun Î·.toFun)
    : LatticeHom K L :=
  have h : Function.Bijective Î·.toFun := by
    apply Function.bijective_iff_has_inverse.mpr
    use invFun
  {
    toFun := invFun
    map_sup' := by
      intro a b
      obtain âŸ¨al, halâŸ© := h.2 a
      obtain âŸ¨bl, hblâŸ© := h.2 b
      subst a; subst b
      rw [â†Î·.map_sup']
      rw [leftInv, leftInv, leftInv]
    map_inf' := by
      intro a b
      simp
      obtain âŸ¨al, halâŸ© := h.2 a
      obtain âŸ¨bl, hblâŸ© := h.2 b
      subst a; subst b
      rw [â†Î·.map_inf']
      rw [leftInv, leftInv, leftInv]
  }

/-!
  (2) The text introduces notation for when `K` has a sublattice that is isomorphic
  to `L`. We will see below that when this happens, then there will be an
  OrderEmbedding.

  (3) Lattice homs need not preserve `âŠ¤` and `âŠ¥` if they exist, apparently.
  So it's natural to define a `BoundedLatticeHom` that is a `LatticeHom` that
  additionally preserves âŠ¤ and âŠ¥. The text calls these `{0, 1}-homomorphisms`.
  These apparently don't show up until chapters 5 and 11.
-/

/-!
  ## 2.18 Examples

  The text returns to various previous examples. The key takeaway is that
  not every `OrderHom` between lattices is also a `LatticeHom`. However, we
  will see below, that every `OrderIso` is a Lattice Isomorphism. This explains
  why we don't need a separate class for Lattice Isomorphisms.
-/

/-!
  ## 2.19 Proposition

  Let `L` and `K` be lattices and `f : L â†’ K` a map.

  (i) The following are equivalent:

  (a) `f` is order-preserving

  (b) âˆ€ a b : L, f a âŠ” f b â‰¤ f (a âŠ” b)

  (c) âˆ€ a b : L, f (a âŠ“ b) â‰¤ f a âŠ“ f b

  In particular, if `f` is a lattice hom, it is order preserving.

  (ii) `f` is a lattice isomorphism if and only if it is an order isomorphism.
-/

lemma example_2_19_i_a_imp_b [Lattice L] [Lattice K] (f : L â†’ K)
    (mono : Monotone f) : âˆ€ a b : L, f a âŠ” f b â‰¤ f (a âŠ” b) := by
  intro a b
  have ale : f a â‰¤ f (a âŠ” b) := mono (le_sup_left : a â‰¤ a âŠ” b)
  have ble : f b â‰¤ f (a âŠ” b) := mono (le_sup_right : b â‰¤ a âŠ” b)
  exact sup_le_iff.mpr âŸ¨ale, bleâŸ©

lemma example_2_19_i_a_imp_c [Lattice L] [Lattice K] (f : L â†’ K)
    (mono : Monotone f) : âˆ€ a b : L, f (a âŠ“ b) â‰¤ f a âŠ“ f b := by
  intro a b
  have lea : f (a âŠ“ b) â‰¤ f a := mono (inf_le_left : a âŠ“ b â‰¤ a)
  have leb : f (a âŠ“ b) â‰¤ f b := mono (inf_le_right : a âŠ“ b â‰¤ b)
  exact le_inf_iff.mpr âŸ¨lea, lebâŸ©

lemma example_2_19_i_b_imp_a [Lattice L] [Lattice K] (f : L â†’ K)
    (h : âˆ€ a b, f a âŠ” f b â‰¤ f (a âŠ” b)) : Monotone f := by
  intro a b le
  have bsup : a âŠ” b = b := sup_of_le_right le; rw[â†bsup]
  have le' : f a â‰¤ f a âŠ” f b := le_sup_left
  exact le_trans le' (h a b)

lemma example_2_19_i_c_imp_a [Lattice L] [Lattice K] (f : L â†’ K)
    (h : âˆ€ a b, f (a âŠ“ b) â‰¤ f a âŠ“ f b) : Monotone f := by
  intro a b le
  have ainf : a âŠ“ b = a := inf_of_le_left le; rw [â†ainf]
  have le' : f a âŠ“ f b â‰¤ f b := inf_le_right
  exact le_trans (h a b) le'

lemma example_2_19_observation [Lattice L] [Lattice K] (f : LatticeHom L K) :
    Monotone f := by apply example_2_19_i_b_imp_a; simp

lemma example_2_19_ii [Nonempty L] [Lattice L] [Lattice K] (f : L â†’ K) :
    (Function.Bijective f âˆ§ âˆƒ hâ‚ : LatticeHom L K, hâ‚.toFun = f) â†”
    âˆƒ hâ‚‚ : L â‰ƒo K, hâ‚‚.toFun = f := by
  constructor
  Â· intro âŸ¨bij, âŸ¨hâ‚, lhomâŸ©âŸ©
    use {
      toFun := f
      invFun := Function.invFun f
      left_inv := Function.leftInverse_invFun bij.1
      right_inv := Function.rightInverse_invFun bij.2
      map_rel_iff' := by
        intro a b
        simp
        constructor <;> intro le
        Â· apply sup_of_le_right at le
          rw [â†lhom, â†hâ‚.map_sup' a b] at le
          suffices a âŠ” b = b by rw [â†this]; exact le_sup_left
          rw [lhom] at le
          exact bij.1 le
        Â· have mono := example_2_19_observation hâ‚
          rw [â†lhom]
          exact mono le }
  Â· intro âŸ¨hâ‚‚, feqâŸ©
    constructor
    Â· rw [â†feq]
      exact OrderIso.bijective hâ‚‚
    Â· use {
        toFun := f
        map_sup' := by
          intro a b
          subst f
          simp
        map_inf' := by
          intro a b
          subst f
          simp
      }

/-!
  # Ideals and filters

  This section introduces lattice ideals and filters. Ideals form a central
  concept in algebra, and filters have a variety of applications in logic
  and topology. Chapter 10 will focus on prime ideals, and this will form
  the basis for the contents of Chapter 11.
-/

/-!
  ## 2.20 Definitions

  Let `L` be a lattice. A non-empty subset `J` of `L` is called an "ideal" if
  and only if

  (i) `âˆ€ a b : L, a âˆˆ J â†’ b âˆˆ J â†’ a âŠ” b âˆˆ J`

  (ii) `âˆ€ a b : L, b âˆˆ J â†’ a â‰¤ b â†’ a âˆˆ J`

  The above definition resembles the definition of an ideal in a ring. We could
  state it more succinctly as a non-empty down-set `J` of a lattice that is closed
  under join.

  Mathlib has `Order.Ideal P` which is a structure that bundles a `LowerSet P`
  together with a proof that the set is "directed" (with respect to `â‰¤`).
  This means that for any `a b âˆˆ J`, there is some `c âˆˆ J` such that `a â‰¤ c âˆ§ b â‰¤ c`.
  Being directed says nothing about closure under `âŠ”`, however, if `L` is a lattice
  and `J : Order.Ideal L`, then it follows that `J` is closed under `âŠ”`.
-/

/-- This is known in Mathlib as `Order.Ideal.sup_mem` -/
lemma example_2_20_lattice_ideal [Lattice L] (J : Order.Ideal L) :
    âˆ€ a âˆˆ J, âˆ€ b âˆˆ J, a âŠ” b âˆˆ J := by
  intro a amem b bmem
  obtain âŸ¨c, âŸ¨hc1, hc2, hc3âŸ©âŸ© := J.directed' a amem b bmem
  have sle : a âŠ” b â‰¤ c := sup_le_iff.mpr âŸ¨hc2, hc3âŸ©
  exact J.lower sle hc1

/-!
  The text says that every ideal of a lattice is a sublattice.
-/

def example_2_20_ideal_toSublattice [Lattice L] (J : Order.Ideal L) : Sublattice L :=
  {
    carrier := J
    supClosed' := by
      intro a amem b bmem
      exact Order.Ideal.sup_mem amem bmem
    infClosed' := by
      intro a amem b _
      exact J.lower' (inf_le_left) amem
  }

/-!
   A dual ideal is called a "filter". More concretely, a non-empty subset `G`
   of a Lattice `L` is called a filter if and only if

   (i) `âˆ€ a b : L, a âˆˆ G â†’ b âˆˆ G â†’ a âŠ“ b âˆˆ G`

   (ii) `âˆ€ a b : L, b âˆˆ G â†’ b â‰¤ a â†’ a âˆˆ G`

   In other words, a filter is a non-empty upper set, closed under `âŠ“`.

   Mathlib calls a filter an `Order.PFilter`, and it is literally defined by
   specifying an `Order.Ideal` on `Láµ’áµˆ`.

   An ideal or filter is called "proper" if it is not equal to `L` itself.
   Mathlib has a type class for this called `Order.Ideal.IsProper J` which
   is a `Prop`. Oddly there is no corresponding definition for `Order.PFilter`.
   Perhaps we are typically meant to work with Ideals in partial orders, and
   when filters are used, we can just invoke the dual?

   It is easy to show that, if a lattice has a `âŠ¤`, then an ideal `J` is proper
   if and only if `âŠ¤ âˆ‰ J`.
-/

lemma example_2_20_withTop_ideal [Lattice L] [OrderTop L] (J : Order.Ideal L) :
    Order.Ideal.IsProper J â†” âŠ¤ âˆ‰ J := by
  constructor <;> intro h
  Â· intro mem
    have allmem : âˆ€ x : L, x âˆˆ J := by
      intro x
      exact J.lower' (le_top (a := x)) mem
    apply h.ne_univ
    ext x
    constructor
    Â· simp
    Â· intro _
      exact allmem x
  Â· exact {
      ne_univ := by
        intro eq
        apply h
        have := Set.mem_univ (âŠ¤ : L)
        rw [â†eq] at this
        exact this }

/-!
  For each `a : L`, the set `â†“áµ–a` is an ideal called the "principal ideal" generated
  by `a`. Dually `â†‘áµ–a` is a principal filter.
-/

def example_2_20_principal_ideal [Lattice L] (a : L) : Order.Ideal L :=
  {
    carrier := â†“áµ–a
    lower' := (â†“áµ–a).lower'
    nonempty' := by use a; simp
    directed' := by
      intro x xmem y ymem
      use x âŠ” y
      constructorm* _ âˆ§ _
      Â· exact sup_le_iff.mpr âŸ¨xmem, ymemâŸ©
      Â· exact le_sup_left
      Â· exact le_sup_right
  }

/-!
  ## 2.21 Examples

  (1) In a finite lattice, every ideal or filter is principal. If `J` is an
  ideal, then `J = â†“áµ–(sSup J)`.
-/

/-- What an absolute mess of a proof! -/
lemma example_2_21_1 [Fintype L] [Lattice L] (J : Order.Ideal L) (x : L) :
    âˆƒ s : L, x âˆˆ J â†” x âˆˆ â†“áµ–s := by
  set J' := Set.toFinset J.carrier with hJ
  have mem_iff : âˆ€ x, x âˆˆ J â†” x âˆˆ J' := by
    intro x
    change x âˆˆ J.carrier â†” x âˆˆ J'
    rw [â†Set.mem_toFinset, â†hJ]
  cases Finset.Nonempty.exists_eq_singleton_or_nontrivial (Set.toFinset_nonempty.mpr J.nonempty') with
  | inl h1 =>
    obtain âŸ¨a, haâŸ© := h1
    use a
    constructor
    Â· intro xmem
      rw [mem_iff] at xmem
      simp_all
    Â· intro xmem
      apply J.lower' at xmem
      have : a âˆˆ â†‘J := by
        rw [mem_iff, hJ, ha]; simp
      exact xmem this
  | inr h1 =>
    obtain âŸ¨a, haâŸ© := J.nonempty'
    rw [â†Set.mem_toFinset] at ha
    set K := J'.erase a with hK
    have add : J' = insert a K := by
      have := (Finset.insert_erase ha).symm
      rwa [hK]
    have ne : (J'.erase a).Nonempty := Finset.Nontrivial.erase_nonempty h1
    rw [â†hK] at ne
    set b := Finset.sup' K ne id with hb
    use b âŠ” a
    constructor
    Â· intro xmem
      by_cases xK : x âˆˆ K
      Â· have xle : x â‰¤ b := by
          rw [hb]
          exact Finset.le_sup' id xK
        apply le_trans xle le_sup_left
      Â· rw [mem_iff x] at xmem
        rw [hK] at xK
        have xeq : x = a := by
          rw [add] at xmem
          rw [Finset.mem_insert_coe] at xmem
          apply Set.eq_or_mem_of_mem_insert at xmem
          cases xmem with
          | inl h => exact h
          | inr h => contradiction
        subst x
        exact le_sup_right
    Â· intro xmem
      rw [mem_iff]
      have mem : b âˆˆ J := by
        apply Finset.sup'_mem
        Â· intro y ymem z zmem
          exact Order.Ideal.sup_mem ymem zmem
        Â· intro i imem
          rw [hK] at imem
          change i âˆˆ J; rw [mem_iff]
          exact Finset.erase_subset a J' imem
      rw [Set.mem_toFinset] at ha
      change b âˆˆ J.carrier at mem
      have bamem := Order.Ideal.sup_mem mem ha
      apply J.lower xmem at bamem
      rw [â†mem_iff]
      exact bamem

/-!
  (2) Let `L` and `K` be bounded lattices and `f : L â†’ K` a bounded lattice hom.
  Then `fâ»Â¹(0)` is an ideal and `fâ»Â¹(1)` is a filter in `L`.
-/

lemma example_2_21_2a [Lattice L] [BoundedOrder L] [Lattice K] [BoundedOrder K]
    (f : BoundedLatticeHom L K) : Order.IsIdeal (fâ»Â¹' {âŠ¥}) :=
  {
    IsLowerSet := by
      intro a b le amem
      simp_all
      have mono : Monotone f := (BoundedLatticeHom.toBoundedOrderHom f).monotone'
      apply_fun â‡‘f at le using mono
      rw [amem] at le
      exact le_bot_iff.mp le
    Nonempty := by
      use âŠ¥
      rw [Set.mem_preimage]
      simp
    Directed := by
      intro a amem b bmem
      simp at amem bmem
      use a âŠ” b
      constructorm* _ âˆ§ _
      Â· simp; tauto
      Â· simp
      Â· simp
  }

/-!
  (3) The following are ideals in `Set X`:

  (a) all subsets not containing a fixed element of `X`

  (b) all finite subsets (this ideal is non-principal if `X` is infinite)
-/

lemma example_2_21_3a (x : X) : Order.IsIdeal { S : Set X | x âˆ‰ S } :=
  {
    IsLowerSet := by
      intro a b le amem xmem
      exact amem (le xmem)
    Nonempty := by
      use âˆ…; simp
    Directed := by
      intro a amem b bmem
      use a âˆª b
      simp_all
  }

lemma example_2_21_3b : Order.IsIdeal { S : Set X | Set.Finite S } :=
  {
    IsLowerSet := by
      intro a b le amem
      exact Set.Finite.subset amem le
    Nonempty := by use âˆ…; simp
    Directed := by
      intro a amem b bmem
      use a âˆª b
      simp_all
  }

/-!
  (4) I won't formalize this one because it's about topological spaces
  and I don't really want to spend too much time learning the surrounding
  API, even if it might be pretty easy.
-/

/-!
  # Complete lattices and â‹‚-structures

  It's time to come back to complete lattices for which the join and
  meet of arbitrary sets `S` exist. Recall that Mathlib denotes these
  as `sSup` and `sInf`, and this is defined via a type class instance
  of `SupSet` or `InfSet`.

  ## 2.22 Lemma

  Let `P` be an ordered set, let `S, T âŠ† P` and assume `sSup S`, `sSupT`,
  `sInf S`, and `sInf T` exist in `P`. (We will just assume `P` is a
  complete lattice instead, because it's a pain to make the right
  assumptions just for the given sets `S` and `T`, even though it's
  more general.)

  (i) `âˆ€ s âˆˆ S, s â‰¤ sSup S âˆ§ sInf S â‰¤ s`.
-/

lemma example_2_22_i [CompleteLattice P] (S : Set P) (s : P) (smem : s âˆˆ S) :
    s â‰¤ sSup S âˆ§ sInf S â‰¤ s := âŸ¨le_sSup smem, sInf_le smemâŸ©

/-!
  (ii) Let `x : P`; then `x â‰¤ sInf S â†” âˆ€ s âˆˆ S, x â‰¤ s`
-/

lemma example_2_22_ii [CompleteLattice P] (S : Set P) (x : P) :
    x â‰¤ sInf S â†” âˆ€ s âˆˆ S, x â‰¤ s := by
  constructor
  Â· intro xle s smem
    exact le_trans xle (sInf_le smem)
  Â· intro h
    exact le_sInf h

/-!
  (iii) Let `x : P`; then `sSup S â‰¤ x â†” âˆ€ s âˆˆ S, s â‰¤ x`
-/

lemma example_2_22_iii [CompleteLattice P] (S : Set P) (x : P) :
    sSup S â‰¤ x â†” âˆ€ s âˆˆ S, s â‰¤ x := by
  constructor
  Â· intro lex s smem
    exact le_trans (le_sSup smem) lex
  Â· intro h
    exact sSup_le h

/-!
  (iv) `sSup S â‰¤ sInf T â†” âˆ€ s âˆˆ S, âˆ€ t âˆˆ T, s â‰¤ t`
-/

lemma example_2_22_iv [CompleteLattice P] (S T : Set P) :
    sSup S â‰¤ sInf T â†” âˆ€ s âˆˆ S, âˆ€ t âˆˆ T, s â‰¤ t := by
  constructor
  Â· intro le s smem t tmem
    apply le_trans _ (sInf_le tmem)
    exact le_trans (le_sSup smem) le
  Â· intro h
    rw [example_2_22_iii]
    intro s smem
    rw [example_2_22_ii]
    intro t tmem
    exact h s smem t tmem

/-!
  (v) If `S âŠ† T`, then `sSup S â‰¤ sSup T` and `sInf T â‰¤ sInf S`
-/

lemma example_2_22_v [CompleteLattice P] (S T : Set P) (sub : S âŠ† T) :
    sSup S â‰¤ sSup T âˆ§ sInf T â‰¤ sInf S := by
  constructor
  Â· rw [example_2_22_iii]
    intro s smem
    specialize sub smem
    exact (example_2_22_i T s sub).1
  Â· rw [example_2_22_ii]
    intro s smem
    specialize sub smem
    exact (example_2_22_i T s sub).2

/-!
  The join and meet behave well with respect to union.

  ## 2.23 Lemma

  Let `P` be a complete lattice and `S, T âŠ† P`. Then
  `sSup (S âˆª T) = (sSup S) âŠ” (sSup T)` and
  `sInf (S âˆª T) = (sInf S) âŠ“ (sInf T)`
-/

lemma example_2_23a [CompleteLattice P] (S T : Set P) :
    sSup (S âˆª T) = (sSup S) âŠ” (sSup T) := by
  rw [â†le_le_iff_eq]
  constructor
  Â· rw [example_2_22_iii]
    intro st stmem
    cases stmem with
    | inl smem => exact le_trans (le_sSup smem) le_sup_left
    | inr tmem => exact le_trans (le_sSup tmem) le_sup_right
  Â· apply sup_le
    Â· exact (example_2_22_v _ _ (Set.subset_union_left)).1
    Â· exact (example_2_22_v _ _ (Set.subset_union_right)).1

lemma example_2_23b [CompleteLattice P] (S T : Set P) :
    sInf (S âˆª T) = (sInf S) âŠ“ (sInf T) := by
  rw [â†le_le_iff_eq]
  constructor
  Â· apply le_inf
    Â· exact (example_2_22_v _ _ (Set.subset_union_left)).2
    Â· exact (example_2_22_v _ _ (Set.subset_union_right)).2
  Â· rw [example_2_22_ii]
    intro st stmem
    cases stmem with
    | inl smem => exact le_trans inf_le_left (sInf_le smem)
    | inr tmem => exact le_trans inf_le_right (sInf_le tmem)

/-!
  ## 2.24 Lemma

  Let `P` be a lattice. Then for every finite, nonempty set `F`,
  `sSup F` and `sInf F` are defined.

  I'm not going to formalize this, because it doesn't play nice with
  the type classes in Mathlib. If I were to do it, I would define
  a new type class that defines functions `fSup : Finset P â†’ P` and
  `fInf : Finset P â†’ P`. The value of this is questionable, however.
  But perhaps I could instead define an instance of `SupSet P` given
  `[Fintype P]` and `[SupSemilattice P]`? Yeah, that could work.

  `TODO`: define an instance of `SupSet P` from `[SupSemilattice P]`
  and `[Fintype P]`

  ## 2.25 Corollary

  Every finite lattice is complete.

  If I had the above formalizations, I could then upgrade them into
  an instance of a `CompleteLattice`.

  `TODO`: define an instance of `CompleteLattice P` from `[Lattice P]`
  and `[Fintype P]`. This might involve altering the above lemmas not
  to rely on `sSup` existing in general.
-/

/-!
  ## 2.26 Definition

  Let `P` and `Q` be partially ordered sets, and `Ï† : P â†’ Q` a map.
  Then we say that `Ï†` "preserves existing joins" if whenever
  `sSup S` exists in `P`, then `sSup (Ï† S)` exists in `Q` and
  `Ï† (sSup S) = sSup (Ï† S)`. The dual definition is obvious.

  This is very similar to Mathlib's `sSupHom` that is a map `Ï†`together
  with a proof that `âˆ€ (s : Set P), Ï† (sSup s) = sSup (Ï† '' s)`. (Here
  `Ï† ''` extends `Ï† : P â†’ Q` to a map `Set P â†’ Set Q`.) The difference
  is that in the text, `sSup` need not be defined on all sets.

  In general, instead of `sSup`, I should just assert `x : P` such that
  `IsLUB S x`, etc.

  `TODO`: Figure out if I can better formalize some of the material in
  Chapter 2 to avoid the use of type classes when they are not warranted.
-/


/-!
  ## 2.27 Lemma

  Let `P` and `Q` be ordered sets and `Ï† : P â†’o Q` be an order preserving
  map.

  (i) Assume that `S âŠ† P` is such that it has a LUB `pu` in `P` and
  `Ï† '' S` has a LUB `qu` in `Q`. Then `qu â‰¤ Ï† pu`. Dually,
  `Ï† pl â‰¤ ql` when the GLBs `pl` and `ql` exists for `S` and `Ï† '' S`
  respectively.
-/

lemma example_2_27_ia [PartialOrder P] [PartialOrder Q] (Ï† : P â†’o Q)
    (S : Set P) {pu : P} {qu : Q} (plub : IsLUB S pu) (qlub : IsLUB (Ï† '' S) qu) :
    qu â‰¤ Ï† pu := by
  rw [isLUB_le_iff qlub]
  intro x xmem
  obtain âŸ¨y, hy1, hy2âŸ© := xmem
  subst x
  mono
  exact plub.1 hy1

/-- Upgrade the above to complete lattices. -/
lemma example_2_27_ia' [CompleteLattice P] [CompleteLattice Q] (Ï† : P â†’o Q)
    (S : Set P) : sSup (Ï† '' S) â‰¤ Ï† (sSup S) :=
  example_2_27_ia Ï† S (isLUB_sSup S) (isLUB_sSup (Ï† '' S))

lemma example_2_27_ib [PartialOrder P] [PartialOrder Q] (Ï† : P â†’o Q)
    (S : Set P) {pl : P} {ql : Q} (pglb : IsGLB S pl) (qglb : IsGLB (Ï† '' S) ql) :
    Ï† pl â‰¤ ql := by
  rw [le_isGLB_iff qglb]
  intro x xmem
  obtain âŸ¨y, hy1, hy2âŸ© := xmem
  subst x
  mono
  exact pglb.1 hy1

lemma example_2_27_ib' [CompleteLattice P] [CompleteLattice Q] (Ï† : P â†’o Q)
    (S : Set P) : Ï† (sInf S) â‰¤ sInf (Ï† '' S) :=
  example_2_27_ib Ï† S (isGLB_sInf S) (isGLB_sInf (Ï† '' S))

/-!
  (ii) Assume now that `Ï† : P â‰ƒo Q` is an order-isomorphism. Then `Ï†`
  preserves all existing joins and meets.
-/

lemma example_2_27_iia [PartialOrder P] [PartialOrder Q] (Ï† : P â‰ƒo Q) :
    âˆ€ S pu, IsLUB S pu â†’ IsLUB (Ï† '' S) (Ï† pu) := by
  intro S pu plub
  constructor
  Â· intro x xmem
    obtain âŸ¨y, hy1, hy2âŸ© := xmem; subst x
    apply Ï†.map_rel_iff'.mpr
    exact plub.1 hy1
  Â· intro x xmem
    rw [â†Ï†.right_inv x]
    apply Ï†.map_rel_iff'.mpr
    rw [isLUB_le_iff plub]
    intro y ymem
    apply Ï†.map_rel_iff'.mp
    simp
    have hy : Ï† y âˆˆ Ï† '' S := by simp_all
    exact xmem hy

lemma example_2_27_iia' [CompleteLattice P] [CompleteLattice Q] (Ï† : P â‰ƒo Q) :
    âˆ€ S, Ï† (sSup S) = sSup (Ï† '' S) := by
  intro S
  have h := example_2_27_iia Ï† S (sSup S) (isLUB_sSup S)
  apply (isLUB_iff_sSup_eq.mp h).symm

lemma example_2_27_iib [PartialOrder P] [PartialOrder Q] (Ï† : P â‰ƒo Q) :
    âˆ€ S pl, IsGLB S pl â†’ IsGLB (Ï† '' S) (Ï† pl) := by
  intro S pl pglb
  constructor
  Â· intro x xmem
    obtain âŸ¨y, hy1, hy2âŸ© := xmem; subst x
    apply Ï†.map_rel_iff'.mpr
    exact pglb.1 hy1
  Â· intro x xmem
    rw [â†Ï†.right_inv x]
    apply Ï†.map_rel_iff'.mpr
    rw [le_isGLB_iff pglb]
    intro y ymem
    apply Ï†.map_rel_iff'.mp
    simp
    have hy : Ï† y âˆˆ Ï† '' S := by simp_all
    exact xmem hy

lemma example_2_27_iib' [CompleteLattice P] [CompleteLattice Q] (Ï† : P â‰ƒo Q) :
    âˆ€ S, Ï† (sInf S) = sInf (Ï† '' S) := by
  intro S
  have h := example_2_27_iib Ï† S (sInf S) (isGLB_sInf S)
  apply (isGLB_iff_sInf_eq.mp h).symm

/-!
  ## 2.28 Lemma

  Let `Q` be a subset, with the induced order, of some ordered set `P`, and
  let `S âŠ† Q`. If `S` has a least upper bound `p` in `P`, then `p âˆˆ Q` and
  `p` is the least upper bound in `Q`.
-/

lemma example_2_28a [PartialOrder P] (Q : Set P) (S : Set Q)
    (p : P) (h : IsLUB (S : Set P) p) (mem : p âˆˆ Q) :
    IsLUB S (âŸ¨p, memâŸ© : Q) := by
  constructor
  Â· intro s smem
    apply Subtype.mk_le_mk.mpr
    exact h.1 (Set.mem_image_of_mem _ smem)
  Â· intro q qmem
    apply Subtype.mk_le_mk.mpr
    apply h.2
    intro x hx
    rw [Set.mem_image] at hx
    obtain âŸ¨x', mem', hx'âŸ© := hx
    subst x
    apply qmem mem'

lemma example_2_28b [PartialOrder P] (Q : Set P) (S : Set Q)
    (p : P) (h : IsGLB (S : Set P) p) (mem : p âˆˆ Q) :
    IsGLB S (âŸ¨p, memâŸ© : Q) := @example_2_28a Páµ’áµˆ _ _ _ _ (IsGLB.dual h) _

/-!
  ## 2.29 Corollary

  Let `ğ”` be a family of subsets of a set `X` and let `{A i | i : Î¹}` be
  a subset of `ğ”`.

  (i) If `â‹ƒ (i : I), A i âˆˆ ğ”`, then it is the least upper bound of the
  family of sets.
-/

lemma example_2_29_i (ğ” : Set (Set X)) (A : Î¹ â†’ Set X)
    (sub : {A i | i : Î¹} âŠ† ğ”) (union : â‹ƒ (i : Î¹), A i âˆˆ ğ”) :
    @IsLUB ğ” _ { A i | i : Î¹ } âŸ¨(â‹ƒ (i : Î¹), A i), unionâŸ© := by
  apply example_2_28a
  constructor
  Â· intro S Smem y ymem
    simp at Smem
    obtain âŸ¨i, eqâŸ© := Smem.1
    rw [Set.mem_iUnion]
    use i; subst S
    exact ymem
  Â· intro S Smem x xmem
    simp [upperBounds] at Smem
    rw [Set.mem_iUnion] at xmem
    obtain âŸ¨i, xmemâŸ© := xmem
    apply Smem i _ xmem
    apply sub
    simp

/-!
  (ii) If `â‹‚ (i : Î¹), {A i | i : Î¹} âˆˆ ğ”` then it is the greatest lower
  bound of the family of sets.
-/

lemma example_2_29_ii (ğ” : Set (Set X)) (A : Î¹ â†’ Set X)
    (sub : {A i | i : Î¹} âŠ† ğ”) (inter : â‹‚ (i : Î¹), A i âˆˆ ğ”) :
    @IsGLB ğ” _ { A i | i : Î¹ } âŸ¨(â‹‚ (i : Î¹), A i), interâŸ© := by
  apply example_2_28b
  constructor
  Â· intro S Smem x xmem
    simp [upperBounds] at Smem
    rw [Set.mem_iInter] at xmem
    obtain âŸ¨i, eqâŸ© := Smem.1; subst S
    exact xmem i
  Â· intro S Smem y ymem
    rw [Set.mem_iInter]
    intro i
    apply Smem _ ymem
    simp
    apply sub
    simp

/-!
  ## 2.30 Lemma

  Let `P` be an ordered set with `sInf` defined for every nonempty
  subset of `P`. (I.e., every nonempty subset has a GLB.) Then
  `sSup` is defined for every subset `S` of `P` that has an upper
  bound in `P`. (In fact, sSup S = sInf (Sáµ˜))
-/

def example_2_30 [PartialOrder P]
    (infs : Î  S : Set P, S.Nonempty â†’ { x // IsGLB S x }) :
    Î  (S : Set P), (Sáµ˜).Nonempty â†’ { y // IsLUB S y } := by
  intro S ne
  obtain âŸ¨x, hx1, hx2âŸ© := infs Sáµ˜ ne
  use x
  constructor
  . intro p pmem
    exact hx2 (Î» a a â†¦ a pmem)
  Â· exact hx1

/-!
  ## 2.31 Theorem

  Let `P` be a non-empty ordered set. Then the following are equivalent.

  (i) `P` is a complete lattice

  (ii) `sInf` exists in `P` for every subset `S` of `P`

  (iii) `P` has a top element `âŠ¤`, and `sInf` exists in `P` for
  every non-empty subset `S` of `P`.
-/

def example_2_31_i_ii (_ : CompleteLattice P) :
    Î  (S : Set P), { x // IsGLB S x } := Î» S â†¦ âŸ¨sInf S, isGLB_sInf SâŸ©

/- lemma example_2_31_i_ii (h : CompleteLattice P) : âˆ€ S : Set P,
    âˆƒ x, IsGLB S x := by
  intro S
  use (sInf S)
  exact isGLB_sInf S  -/

def example_2_31_ii_iiia [PartialOrder P]
    (h : Î  (S : Set P), {x // IsGLB S x}) : OrderTop P :=
  {
    top := (h âˆ…).val
    le_top := by
      intro a
      have h2 := (h âˆ…).property
      simp at h2
      apply h2
  }

def example_2_31_ii_iiib [PartialOrder P]
    (h : Î  (S : Set P), {x // IsGLB S x}) :
    Î  (S : Set P), S.Nonempty â†’ {x // IsGLB S x} := Î» S _ â†¦ h S

/- lemma example_2_31_ii_iii' [PartialOrder P] (h : âˆ€ S : Set P, âˆƒ x, IsGLB S x) :
    (âˆƒ _ : OrderTop P, true) âˆ§ âˆ€ S : Set P, S.Nonempty â†’ âˆƒ x, IsGLB S x := by
  constructor
  Â· obtain âŸ¨x, hxâŸ© := h âˆ…
    use {
      top := x
      le_top := by
        intro a
        obtain âŸ¨_, hx2âŸ© := hx
        simp at hx2
        apply hx2
        simp
    }
  Â· intro S _
    exact h S  -/

def example_2_31_ii_i [PartialOrder P]
    (h : Î  S : Set P, {x // IsGLB S x}) : CompleteLattice P :=
  have h' := example_2_31_ii_iiib h
  haveI : OrderTop P := example_2_31_ii_iiia h
  have supne : âˆ€ S : Set P, (Sáµ˜).Nonempty := by
    intro S
    use âŠ¤
    intro a _
    exact le_top
  {
    inf := Î» a b â†¦ h (Set.insert a {b})
    sup := Î» a b â†¦ example_2_30 h' ({a, b}) (supne {a, b})
    inf_le_left := by
      intro a b
      have h1 := (h (Set.insert a {b})).property
      apply h1.1
      exact Set.mem_insert a {b}
    inf_le_right := by
      intro a b
      have h1 := (h (Set.insert a {b})).property
      apply h1.1
      apply Set.mem_insert_of_mem
      simp
    le_sup_left := by
      intro a b
      have h1 := (example_2_30 h' (Set.insert a {b}) (supne {a, b})).property
      apply h1.1
      exact Set.mem_insert a {b}
    le_sup_right := by
      intro a b
      have h1 := (example_2_30 h' (Set.insert a {b}) (supne {a, b})).property
      apply h1.1
      apply Set.mem_insert_of_mem
      simp
    sup_le := by
      intro a b c ac bc
      have h1 := (example_2_30 h' (Set.insert a {b}) (supne {a, b})).property
      apply h1.2
      intro x hx
      cases hx <;> subst x <;> assumption
    le_inf := by
      intro c a b ca cb
      have h1 := (h (Set.insert a {b})).property
      apply h1.2
      intro x hx
      cases hx <;> subst x <;> assumption
    sInf := Î» S â†¦ h S
    sSup := Î» S â†¦ example_2_30 h' S (supne S)
    sInf_le := by
      intro S a amem
      have h1 := (h S).property
      simp
      exact h1.1 amem
    le_sInf := by
      intro S a ble
      have h1 := (h S).property
      simp
      apply h1.2
      exact ble
    le_sSup := by
      intro S a amem
      have h1 := (example_2_30 h' S (supne S)).property
      simp
      exact h1.1 amem
    sSup_le := by
      intro S a ble
      have h1 := (example_2_30 h' S (supne S)).property
      simp
      apply h1.2
      exact ble
    bot := example_2_30 h' âˆ… (supne âˆ…)
    le_top := this.le_top
    bot_le := by
      intro x
      have h1 := (example_2_30 h' âˆ… (supne âˆ…)).property
      simp
      apply h1.2
      simp
  }

noncomputable
def example_2_31_iii_ii [PartialOrder P] [ot : OrderTop P]
    (h : Î  S : Set P, S.Nonempty â†’ { x // IsGLB S x }) :
    Î  S : Set P, { x // IsGLB S x } := Î» S â†¦ by
  by_cases ne : S.Nonempty
  Â· exact h S ne
  Â· rw [Set.nonempty_iff_ne_empty] at ne
    simp at ne
    subst S
    have : IsGLB (âˆ… : Set P) âŠ¤ := by simp
    exact âŸ¨âŠ¤, thisâŸ©

noncomputable
def example_2_31_iii_i [PartialOrder P] [ot : OrderTop P]
  (h : Î  S : Set P, S.Nonempty â†’ { x // IsGLB S x }) :
  CompleteLattice P := by
  have h' := example_2_31_iii_ii h
  exact example_2_31_ii_i h'

/-!
  ## 2.32 Corollary

  Let `X` be a set and let `ğ”` be a family of subsets ordered by inclusion
  such that

  (a) `âˆ€ Î¹ (A : Î¹ â†’ Set X), { A i | i : Î¹} âŠ† ğ” â†’ â‹‚ (i : Î¹), A i âˆˆ ğ”`

  (b) `X âˆˆ ğ”`.

  Then ğ” is a complete lattice in which `â¨… (i : Î¹), A i = â‹‚ (i : Î¹), A i`
  and `â¨† (i : Î¹), A i = â‹ƒâ‚€ { B âˆˆ ğ” | â‹ƒ (i : Î¹), A i âŠ† B}`
-/

noncomputable
def example_2_32 (ğ” : Set (Set X)) (h1 : (Set.univ : Set X) âˆˆ ğ”)
    (h2 : âˆ€ (Î¹ : Type 0) [Inhabited Î¹] (A : Î¹ â†’ Set X), { A i | i : Î¹ } âŠ† ğ” â†’ â‹‚ (i : Î¹), A i âˆˆ ğ”) :
    CompleteLattice ğ” := by
  have ot : OrderTop ğ” :=
    {
      top := âŸ¨(Set.univ : Set X), h1âŸ©
      le_top := by
        intro a x _; simp
    }
  have h3 : Î  S : Set ğ”, S.Nonempty â†’ { x // IsGLB S x } := by
    intro S nes
    set A : S â†’ Set X := Î» X â†¦ X.val with hA
    have HA : âˆ€ S, A â†‘S = â†‘â†‘S := by
      intro S1
      rw [hA]
    have sub : { A i | i : S } âŠ† ğ” := by
      intro x âŸ¨i, hiâŸ©; subst x
      rw [hA]; simp
    have : Nonempty S := Set.Nonempty.to_subtype nes
    inhabit â†¥S
    have g := h2 â†¥S A sub
    refine' âŸ¨âŸ¨â‹‚ (i : S), A i, gâŸ©, _âŸ©
    constructor
    Â· intro x xmem y ymem
      simp at ymem âŠ¢
      set x' : S := âŸ¨x, xmemâŸ© with Hx'
      have hx' := HA x'
      specialize ymem â†‘â†‘x' (Subtype.mem x) xmem
      rw [hx', Hx'] at ymem
      exact ymem
    Â· intro x xmem y ymem i imem
      obtain âŸ¨a, b, haâŸ© := imem
      beta_reduce
      have ha := Subtype.mem a
      specialize xmem ha
      specialize xmem ymem
      rw [HA a]
      exact xmem
  exact example_2_31_iii_i h3

/-!
  ## 2.33 Definitions

  If `ğ”` is a non-empty family of subsets of `X` which satisfies condition (a) of
  `example_2_32` (i.e., `h2` in the lemma statement), then `ğ”` is called an
  "intersection structure". If `ğ”` also satisfies (b) (i.e., `h1`) then it is
  called a "topped intersection structure".

  Mathlib does not seem to have classes for these concepts. It would be instructive
  to make some here, since the text is likely to return to them frequently enough.
  I will probably not build a robust API around them, however, unless that becomes
  more obviously helpful.
-/

class InterStructure (ğ” : Set (Set X)) where
  ne : ğ”.Nonempty
  inter : âˆ€ (Î¹ : Type 0) [Inhabited Î¹] (A : Î¹ â†’ Set X), { A i | i : Î¹ } âŠ† ğ” â†’ â‹‚ (i : Î¹), A i âˆˆ ğ”

class ToppedInterStructure (ğ” : Set (Set X)) extends InterStructure ğ” where
  univ_mem : (Set.univ : Set X) âˆˆ ğ”

/-!
  Just to test that these classes are working ok, I will prove 2.32 again for
  `ToppedInterStructure`s.
-/

noncomputable
def example_2_33 {ğ” : Set (Set X)} [Inst : ToppedInterStructure ğ”] : CompleteLattice ğ” :=
  example_2_32 ğ” Inst.univ_mem Inst.inter

/-!
  ## 2.34 Examples

  (1) Consider `X â†’ Option Y` where we assume `X.Nonempty` and `Y.nonempty`. From observations
  in 1.10, the map `Î» Ï€ â†¦ graph Ï€` is an order-embedding of `X â†’ Option Y` into `Set (X Ã— Y)`.
  Let `ğ”` be the family of subsets of `X Ã— Y` which are graphs of partial maps. To prove that
  `ğ”` is cloased under intersections, use the characterization given in 1.10: if
  `S âŠ† X Ã— Y`, then `S âˆˆ ğ”` if and only if `(s, y) âˆˆ S` and `(s, y') âˆˆ S` imply `y = y'`.
  So we can make `ğ”` a (topless) InterStructure. If `âˆ£Yâˆ£ = 1` it is actually topped.
-/
@[simp]
def graph {Y : Type} (f : X â†’ Option Y) : Set (X Ã— Y) := { (x, y) | f x = some y }

def example_2_34_1b {Y : Type} : (X â†’ Option Y) â†ªo Set (X Ã— Y) :=
  {
    toFun := Î» Ï€ â†¦ graph Ï€
    inj' := by
      intro f g eq
      ext x y
      constructor <;> intro mem
      Â· have xymemf : (x, y) âˆˆ graph f := by simpa
        simp_all
      Â· have xymemf : (x, y) âˆˆ graph f := by simp_all
        simp at xymemf; assumption
    map_rel_iff' := by
      intro f g
      constructor <;> intro le
      Â· simp at le
        intro x eq
        rw [Option.isSome_iff_exists] at eq
        obtain âŸ¨y, hyâŸ© := eq
        specialize le x y hy
        rw [hy, le]
      Â· intro xy xymem
        simp_all
        have is : Option.isSome (f xy.1) = true := by
          rw [Option.isSome_iff_exists]
          use xy.2
        specialize @le xy.1 is
        rw [â† le]; assumption
  }

lemma example_2_34_1c {Y : Type} (S : Set (X Ã— Y)) :
    (âˆ€ x y y', (x, y) âˆˆ S â†’ (x, y') âˆˆ S â†’ y = y') â†” âˆƒ Ï€ : X â†’ Option Y, S = graph Ï€ := by
  constructor
  Â· intro h
    refine âŸ¨?_, ?_âŸ©
    Â· intro x
      by_cases e : âˆƒ y, (x, y) âˆˆ S
      Â· let a := Exists.choose e
        exact some a
      Â· exact none
    Â· ext âŸ¨x, yâŸ©
      constructor
      Â· intro mem
        dsimp only [graph, Set.mem_setOf_eq]
        split_ifs with e
        Â· simp
          apply h x e.choose y e.choose_spec mem
        Â· apply e âŸ¨y, memâŸ©
      Â· intro mem
        simp only [graph, Option.dite_none_right_eq_some, Option.some.injEq,
            Set.mem_setOf_eq] at mem
        obtain âŸ¨e, heâŸ© := mem
        rw [â†he]
        exact e.choose_spec
  Â· intro âŸ¨Ï€, hâŸ© x y y' mem1 mem2
    simp_all

def example_2_34_1 {Y : Type} [Inhabited X] [Inhabited Y]
    : InterStructure { graph Ï€ | Ï€ : X â†’ Option Y } :=
  {
    ne := âŸ¨{}, Î» _ â†¦ none, by simpâŸ©
    inter := by
      simp only [Set.setOf_subset_setOf, forall_exists_index, forall_apply_eq_imp_iff,
        Set.mem_setOf_eq]
      -- Let {Sáµ¢} be a family of sets such that each one is the graph of some Ï€áµ¢
      intro Î¹ inh S sub
      -- Show there is some partial function Ï€ whose graph is â‹‚ i, Sáµ¢
      refine âŸ¨?_, ?_âŸ©
      -- We define Ï€ as follows
      Â· intro x
        -- For any x, decide if there is a y so that all the Sáµ¢ have (x, y) as a member.
        by_cases e : âˆƒ y, (âˆ€ i, (x, y) âˆˆ S i)
          -- If so, define Ï€(x) to be such a y (using Exists.choose)
        Â· exact some e.choose
          -- Otherwise, Ï€(x) is undefined
        Â· exact none
      -- We now prove that graph Ï€ = â‹‚ i, Sáµ¢
      Â· ext âŸ¨x, yâŸ©
        constructor
        -- Assume (x, y) âˆˆ graph Ï€
        Â· intro eq
          -- Let Sáµ¢ be any set of the family. We must show (x, y) âˆˆ Sáµ¢
          intro Si imem
          obtain âŸ¨i, hiâŸ© := imem
          rw[â†hi]; dsimp only
          -- Since (x, y) âˆˆ graph Ï€, the choice function gives a y', s.t. (x, y') is in each Sáµ¢
          -- and y' = y
          simp only [graph, Option.dite_none_right_eq_some, Option.some.injEq,
              Set.mem_setOf_eq] at eq
          obtain âŸ¨e, heâŸ© := eq
          have hec := e.choose_spec i
          -- Since (x, y') âˆˆ Sáµ¢ and y' = y, (x, y) âˆˆ Sáµ¢
          rwa [he] at hec
        -- Conversely, assume (x, y) âˆˆ â‹‚ i, Sáµ¢
        Â· intro con; simp only [Set.mem_iInter] at con
          -- To show that (x, y) âˆˆ graph Ï€, we must show that there is a y' s.t.
          -- (x, y') is in each Sáµ¢ and that y' = y.
          simp only [graph, Option.dite_none_right_eq_some, Option.some.injEq, Set.mem_setOf_eq]
          refine âŸ¨?_, ?_âŸ©
          -- We first show there is a y' s.t. (x, y') in in each Sáµ¢, with y as witness
          Â· use y
          -- We now show that the choice function is unique, i.e. we get the y we need
          Â· let e : âˆƒ y, âˆ€ i, (x, y) âˆˆ S i := âŸ¨y, conâŸ©
            -- Since Î¹ is inhabited, we know the choice function gives us y' s.t.
            -- (x, y') âˆˆ Sáµ¢ for some default i
            obtain h :=  e.choose_spec default
            -- And we know (x, y) âˆˆ Sáµ¢ as well, by assumption
            specialize con default
            -- But since Sáµ¢ is the graph of some Ï€áµ¢, (x, y) and (x, y') are in the same graph
            obtain âŸ¨p, hpâŸ© := sub default
            unfold graph at hp
            rw [â†hp] at h con
            -- So y' = y
            simp [Set.mem_setOf_eq] at h con
            rw [h] at con
            rwa [Option.some.injEq] at con
  }

/-!
  (2) We decline to formalize this list as it involves other mathematical structures
  beyond the scope of the current project.

  (3) Same here.

  (4) Same here.

  Given a `PartialOrder P` and a map `F : P â†’ P` an element `x âˆˆ P` is called a
  "fixpoint" of `F` if and only if `F x = x`. Fixpoints are treated in detail in
  Chapter 8. For now we state and prove the following famous theorem.
-/

/-!
  ## 2.35 The Knaster-Tarski Fixpoint Theorem

  Let `L` be a complete lattice and `F : L â†’ L` an order preserving map. Then
  `Î± := sSup { x : L | x â‰¤ F x }` is a fixpoint of `F`. Further, `Î±` is the
  greatest fixpoint of `F`. Dually, `F` has a least fixpoint given by
  `Î² := sInf { x : L | F x â‰¤ x }`.
-/

theorem knasterTarskiFixpoint_a [CompleteLattice L] (F : L â†’o L) :
    F (sSup { x | x â‰¤ F x }) = sSup { x | x â‰¤ F x } := by
  let H := { x | x â‰¤ F x }
  let A := sSup H
  have h : âˆ€ x âˆˆ H, x â‰¤ A âˆ§ x â‰¤ F x âˆ§ F x â‰¤ F A :=
    Î» x mem â†¦ âŸ¨le_sSup mem, mem, F.monotone (le_sSup mem)âŸ©
  have A_le : A â‰¤ F A := sSup_le Î» x mem â†¦ (h x mem).2.1.trans (h x mem).2.2
  have FA_le : F A â‰¤ A := le_sSup (F.monotone A_le)
  exact le_le_iff_eq.mp âŸ¨FA_le, A_leâŸ©

theorem knasterTarskiFixpoint_b [CompleteLattice L] (F : L â†’o L) :
    âˆ€ x, (F x = x) â†’ x â‰¤ sSup { x | x â‰¤ F x } := by
  intro x fp
  symm at fp
  apply le_of_eq at fp
  exact le_sSup fp

theorem knasterTarskiFixpoint_dual_a [CompleteLattice L] (F : L â†’o L) :
    F (sInf { x | F x â‰¤ x }) = sInf { x | F x â‰¤ x } := by
  exact @knasterTarskiFixpoint_a Láµ’áµˆ _ (OrderHom.dual F)

theorem knasterTarskiFixpoint_dual_b [CompleteLattice L] (F : L â†’o L) :
    âˆ€ x, (F x = x) â†’ sInf { x | F x â‰¤ x } â‰¤ x := by
  exact @knasterTarskiFixpoint_b Láµ’áµˆ _ (OrderHom.dual F)

/-!
  These are indeed in Mathlib, albeit in a slightly different form. Mathlib has
  `OrderHom.gfp F` that is an OrderHom from monotone maps on `L` to `L`. It
  maps `F` to `sSup { x | x â‰¤ F x }`. Similarly, it has `OrderHom.lfp F` that
  takes `F` to its least fixed point. To show this is a fixed point,
  it has `OrderHom.isFixedPt_lfp`. It also has the fact it is the least fixed
  point by `OrderHom.isLeast_lfp` and also `OrderHom.lfp_le_fixed`. Oddly,
  there is no equivalent `OrderHom.fixed_le_gfp` although it does have
  `OrderHom.isGreatest_gfp`.
-/

/-!
  # Chain conditions and completeness

  I skipped the proof that every finite lattice is complete. However, there are
  weaker finiteness conditions that already guarantee a lattice is complete. This
  section introduces those.
-/

/-!
  ## 2.37 Definitions

  Let `P` be an ordered set.

  (i) If `C = {câ‚€, câ‚, câ‚™}` is a finite chain in `P` with `âˆ£Câˆ£ = n + 1` then we
  say that the "length" of `C` is `n`. (I.e., it's the number of uses of `â‹–`.)

  (ii) `P` is said to have "length" `n`, written `â„“(P) = n`, if the length of
  the longest chain in `P` is `n`.

  (iii) `P` is of "finite length" if it has length `n` for some `n : â„•`.

  (iv) `P` has "no infinite chains" if every chain in `P` is finite.

  (v) `P` satisfies the "ascending chain condition", (ACC), if and only if
  given any sequence `xâ‚ â‰¤ xâ‚‚ â‰¤ ... â‰¤ xâ‚™ â‰¤ ...` of elements of `P`, there
  exists `k : â„•` such that `xâ‚– = xâ‚–â‚Šâ‚ = ...`. The dual of the ascending
  chain condition is the descending chain condition (DCC).

  Mathlib has `Set.chainHeight : Set P â†’ â„•âˆ` which is supremum over
  all finite chains `l`, of the length of `l`. This is essentially (ii)
  above (except it works on `Set P`, so we have to feed it `Set.univ`).
  We can use this to define a predicate saying `P` is of finite length,
  giving us (iii). An infinite chain in `P` is essentially (the image of)
  an order embedding of â„• into `P`. We can thus express (iv) by saying that
  there does not exist an OrderEmbedding of â„• into `P`.
  The ACC or DCC does not seem to be defined
  explicitly in Mathlib, however, there is `WellFounded.monotone_chain_condition`
  which says that `(â¬ > â¬)` is well founded if and only if
  `âˆ€ (a : â„• â†’o P), âˆƒ (n : â„•), âˆ€ (m : â„•), n â‰¤ m â†’ a n = a m`. That means that
  we can usually just substitute `WellFounded (Â· > Â·)` for ACC. But for
  this study, we will express it explicitly, and use the above to prove the
  equivalence. We also explicitly spell out the DCC in the definition
  and show that it is the same (by `rfl`) as the ACC in the dual order.
-/

noncomputable def Order.length (P : Type) [PartialOrder P] : â„•âˆ :=
  Set.chainHeight (Set.univ : Set P)

def Order.FiniteLength (P : Type) [PartialOrder P] : Prop :=
  Order.length P â‰  âŠ¤

@[reducible]
def Order.NoInfiniteChains' (P : Type) [PartialOrder P] : Prop :=
  Â¬âˆƒ _ : â„• â†ªo P, true

@[reducible]
def Order.NoInfiniteChains (P : Type) [PartialOrder P] : Prop :=
  âˆ€ Q : Set P, IsChainLE â†‘Q â†’ Finite Q

@[reducible]
def Order.ACC (P : Type) [PartialOrder P] : Prop :=
  âˆ€ (f : â„• â†’o P), âˆƒ n : â„•, âˆ€ (m : â„•), n â‰¤ m â†’ f n = f m

@[reducible]
def Order.DCC (P : Type) [PartialOrder P] : Prop :=
  âˆ€ (f : â„• â†’o Páµ’áµˆ), âˆƒ n : â„•, âˆ€ (m : â„•), n â‰¤ m â†’ f n = f m

lemma Order.ACC_dual_iff (P : Type) [PartialOrder P] :
    Order.ACC Páµ’áµˆ â†” Order.DCC P := by rfl

lemma Order.DCC_dual_iff (P : Type) [PartialOrder P] :
    Order.DCC Páµ’áµˆ â†” Order.ACC P := by rfl

lemma WellFoundedGT_iff_ACC [PartialOrder P] :
    WellFounded ((Â· > Â·) : P â†’ P â†’ Prop) â†” Order.ACC P :=
  WellFounded.monotone_chain_condition

lemma WellFoundedLT_iff_DCC [PartialOrder P] :
    WellFounded ((Â· < Â·) : P â†’ P â†’ Prop) â†” Order.DCC P :=
  @WellFounded.monotone_chain_condition Páµ’áµˆ _

lemma Order.NoInfiniteChains.Dual {P : Type} [PartialOrder P] :
    Order.NoInfiniteChains P â†’ Order.NoInfiniteChains Páµ’áµˆ := by
  simp [NoInfiniteChains]
  intro h Q ch
  set Q' := { OrderDual.ofDual q | q âˆˆ Q } with hQ
  have : Q â‰ƒ Q' := by
    refine Set.BijOn.equiv ?f ?h
    Â· intro pd; exact OrderDual.ofDual pd
    Â· constructor
      Â· intro p
        simp [Q']
      Â· constructor
        Â· intro a amem b bmem
          simp
        Â· intro a amem
          simp [Q'] at amem âŠ¢
          assumption
  have ch' : IsChainLE Q' := by
    simp [IsChainLE, IsChain] at ch âŠ¢
    intro a amem b bmem ne
    have amem' : OrderDual.toDual a âˆˆ Q := by simp [Q'] at amem; assumption
    have bmem' : OrderDual.toDual b âˆˆ Q := by simp [Q'] at bmem; assumption
    specialize ch amem' bmem' ne
    exact id (Or.symm ch)
  specialize h { OrderDual.ofDual q | q âˆˆ Q } ch'
  rw [â†hQ] at h
  exact (Equiv.finite_iff (id this.symm)).mp h

lemma Order.NoInfiniteChains.Dual_iff {P : Type} [PartialOrder P] :
    Order.NoInfiniteChains P â†” Order.NoInfiniteChains Páµ’áµˆ :=
  âŸ¨Order.NoInfiniteChains.Dual, Order.NoInfiniteChains.DualâŸ©

lemma Order.no_strict_inc_of_ACC [PartialOrder P] (acc : Order.ACC P) (f : P â†’ P) (p : P) :
    Â¬âˆ€ p, p < f p := by
  by_contra h
  let g : â„• â†’ P := Nat.rec p fun _ â†¦ f
  have hg : âˆ€ n, g n < g (n + 1) := by intro n; exact h (g n)
  let G : â„• â†’o P := âŸ¨g, StrictMono.monotone <| strictMono_nat_of_lt_succ hgâŸ©
  obtain âŸ¨n, hnâŸ© := acc G
  specialize hn (n + 1) (by simp)
  specialize hg n
  have : âˆ€ n, G n = g n := by intro n; rfl
  rw [this n, this (n + 1)] at hn
  rw [hn] at hg
  exact (lt_self_iff_false (g (n + 1))).mp hg

lemma Order.no_strinct_dec_of_DCC [PartialOrder P] (dcc : Order.DCC P) (f : P â†’ P) (p : P) :
    Â¬âˆ€ p, f p < p := Order.no_strict_inc_of_ACC dcc f p

lemma Order.no_strict_inc_on_of_ACC [PartialOrder P] (acc : Order.ACC P) (A : Set P) (f : A â†’ A) (p : A) :
    Â¬âˆ€ p, p < f p := by
  by_contra h
  let g : â„• â†’ A := Nat.rec p fun _ â†¦ f
  have hg : âˆ€ n, g n < g (n + 1) := by intro n; exact h (g n)
  let G : â„• â†’o P := âŸ¨Î» a â†¦ (g a).val, StrictMono.monotone <| strictMono_nat_of_lt_succ hgâŸ©
  obtain âŸ¨n, hnâŸ© := acc G
  specialize hn (n + 1) (by simp)
  specialize hg n
  have hg' : (g n).val < (g (n + 1)).val := by exact h (g n)
  have : âˆ€ n, G n = (g n).val := by intro n; rfl
  rw [this n, this (n + 1)] at hn
  rw [hn] at hg'
  apply (lt_self_iff_false (g (n + 1))).mp hg'

lemma Order.no_strict_dec_on_of_DCC [PartialOrder P] (dcc : Order.DCC P) (A : Set P) (f : A â†’ A) (p : A) :
    Â¬âˆ€ p, f p < p := Order.no_strict_inc_on_of_ACC dcc A f p



/-!
  ## 2.38 Examples

  (1) A lattice of finite length has no infinite chains, and so satisfies both ACC
  and DCC. (This is one of those deceptively hard things to work with.)
-/

lemma example_2_38_1a [Finite P] [PartialOrder P] : Order.ACC P := by
  rw [â†WellFoundedGT_iff_ACC, â†isWellFounded_iff]
  exact @Finite.to_wellFoundedGT P _ _

lemma example_2_38_1b [Finite P] [PartialOrder P] : Order.DCC P :=
  @example_2_38_1a Páµ’áµˆ _ _

/-!
  (2) The lattice of â„• under the divides relation satisfies DCC but not ACC.

  I forget where this lattice instance is defined. So I skip it for now.

  TODO: Define a type synonym for â„• that has the lattice structure
  for divides on it.

  (3) â„• under the normal order satisfies DCC but not ACC. Dually, â„•áµ’áµˆ satisfies
  ACC but not DCC.
-/

lemma example_2_38_3a : Order.DCC â„• := by
  rw [â†WellFoundedLT_iff_DCC]
  exact wellFounded_lt

lemma example_2_38_3b : Â¬ Order.ACC â„• := by
  simp [Order.ACC]
  use âŸ¨Î» n â†¦ 2^n, by apply pow_right_monoâ‚€; simpâŸ©
  intro n
  use n + 1, by simp, by simp

lemma example_2_38a_dual : Order.ACC â„•áµ’áµˆ := example_2_38_3a

lemma example_2_38b_dual : Â¬Order.DCC â„•áµ’áµˆ := example_2_38_3b

/-!
  (4) This is about dimensions of vector spaces which I will skip for now.
-/

/-!
  ## 2.39 Lemma

  A partial order `P` satisfies ACC if and only if every non-empty
  subset `A` of `P` has a maximal element.

  The text defers a formal proof until chapter 10, but I tackle it here.
  They note that the proof requires the axiom of choice.
-/

lemma example_2_39 [PartialOrder P] : Order.ACC P â†”
    âˆ€ A : Set P, A.Nonempty â†’ âˆƒ a, Maximal (Â· âˆˆ A) a := by
  constructor
  Â· contrapose
    -- Assume there is a nonempty set A with no maximal element, and that ACC holds.
    intro nmax acc
    push_neg at nmax
    simp only [Order.ACC] at acc
    obtain âŸ¨A, ne, hâŸ© := nmax
    -- Since A is nonempty, it has some element p.
    obtain âŸ¨p, hpâŸ© := ne
    simp only [Maximal, not_and, not_forall, Classical.not_imp] at h
    -- We mimic the Mathlib proof for orders with no maximal elements.
    -- This is given by the typeclass NoMaxOrder, which A satisfies.
    have nmo : NoMaxOrder A := by
      constructor
      intro a
      obtain âŸ¨b, hb1, hb2, hb3âŸ© := h a <| Subtype.coe_prop a
      exact âŸ¨âŸ¨b, hb1âŸ©, lt_of_le_not_le hb2 hb3âŸ©
    -- Using the axiom of choice, we can construct a strictly increasing function g: A â†’ A
    choose g hg using fun x : A => exists_gt x
    -- From g, we construct f: â„• â†’ A by f 0 = p, f (n + 1) = f (g n).
    let f : â„• â†’ A := Nat.rec âŸ¨p, hpâŸ© fun _ â†¦ g
    -- Since g is strictly monotone, so is f,
    have smf : StrictMono f := strictMono_nat_of_lt_succ fun n â†¦ hg _
    -- and so it must also be (weekly) monotone.
    have mf : Monotone f := by exact StrictMono.monotone smf
    -- Thus we can promote f to the order homomorphism needed by ACC.
    let h : â„• â†’o P :=
    {
      toFun := Î» n => (f n).1
      monotone' := by
        intro a b le
        apply mf le
    }
    -- From ACC we obtain an n : â„•, after which h becomes constant
    obtain âŸ¨n, hnâŸ© := acc h
    -- Since h is strictly monotone, this will be a contradiction.
    -- In particular, consider n and n + 1. By ACC h n = h (n + 1).
    specialize hn (n + 1) (by simp)
    -- But by monotonicity of h, h n < h (n + 1)
    have hn' : h n < h (n + 1) := smf (by simp : n < n + 1)
    -- This contradicts irreflexivity of <.
    rw [hn] at hn'
    exact lt_irrefl _ hn'
  Â· contrapose
    -- Assume ACC doesn't hold, and prove there is a set without a maximal element.
    intro acc; push_neg
    simp only [Order.ACC, not_forall, not_exists, Classical.not_imp] at acc
    -- Since ACC doesn't hold, there is a monotone f that doesn't stabilze.
    obtain âŸ¨f, hfâŸ© := acc
    -- We take the range of f to be the set without a maximal element.
    use (Set.range f), âŸ¨f 0, by simpâŸ©
    -- To show there's no maximal element, consider any element a, and assume it's maximal.
    intro a âŸ¨âŸ¨N, hNâŸ©, h2âŸ©
    -- Using the property of f, we get M > N (where f N = a) and f N â‰  f M
    obtain âŸ¨M, hM, eqâŸ© := hf N
    -- Since f is monotone, we know f N â‰¤ f M.
    have le := f.monotone' hM
    -- But since, a = f N is assumed maximal, this implies f M â‰¤ f N
    specialize h2 (by simp : f M âˆˆ Set.range f)
    rw [â†hN] at h2
    specialize h2 le
    -- But this means, f M = f N, contradicting the definition of M witnessing instability of f.
    exact eq <| eq_of_le_of_le le h2

/-- The following alternative proof uses the built-in `WellFounded.wellFounded_iff_has_min`
    together with the proof above that `ACC` and `WellFounded` are equivalent. The API for
    WellFounded is quite robust, so it is advantageous to convert to WellFounded whenever
    possible. -/
lemma example_2_39' [PartialOrder P] : Order.ACC P â†”
    âˆ€ A : Set P, A.Nonempty â†’ âˆƒ a, Maximal (Â· âˆˆ A) a := by
  rw [â†WellFoundedGT_iff_ACC]
  constructor
  Â· intro wf
    rw [WellFounded.wellFounded_iff_has_min] at wf
    intro A ne
    obtain âŸ¨m, mem, hmâŸ© := wf A ne
    use m
    use mem
    intro y hy le
    specialize hm y hy
    apply le_of_eq
    exact eq_iff_le_not_lt.mpr âŸ¨le, hmâŸ©|>.symm
  Â· rw [WellFounded.wellFounded_iff_has_min]
    intro max A ne
    obtain âŸ¨m, mem, hmâŸ© := max A ne
    use m, mem
    intro a amem gt
    specialize hm amem (le_of_lt gt)
    rw [gt_iff_lt] at gt
    exact not_lt_of_le hm <| gt

/-- Here is an alternate proof of the forward direction that leverages the
    result above saying that ACC is incompatible with any function
    f : A â†’ A such that A âŠ† P and âˆ€ a, a < f a. -/
lemma example_2_39a [PartialOrder P] : Order.ACC P â†’
    âˆ€ A : Set P, A.Nonempty â†’ âˆƒ a, Maximal (Â· âˆˆ A) a := by
  contrapose
  intro nmax acc
  push_neg at nmax
  obtain âŸ¨A, âŸ¨p, hpâŸ©, hâŸ© := nmax
  simp only [Maximal, not_and, not_forall, Classical.not_imp] at h
  choose g hg using h
  let f : A â†’ A := Î» a â†¦ âŸ¨g a.1 a.2, (hg a.1 a.2).chooseâŸ©
  apply Order.no_strict_inc_on_of_ACC acc A f âŸ¨p, hpâŸ©
  intro âŸ¨q, hqâŸ©
  simp only [f]
  obtain âŸ¨mem, âŸ¨le, nleâŸ©âŸ© := hg q hq
  exact lt_of_le_not_le le nle

/-!
  ## 2.40 Theorem

  A partial order `P` has no infinite chains, if and only if it satisfies
  both ACC and DCC.
-/

theorem example_2_40mp [PartialOrder P] :
    Order.NoInfiniteChains P â†’ Order.ACC P âˆ§ Order.DCC P := by
  intro h
  constructor
  Â· intro f
    by_contra h1
    push_neg at h1
    let h2 : âˆ€ p : Set.range f, âˆƒ q, p < q := by
      intro âŸ¨p, âŸ¨n, hnâŸ©âŸ©
      obtain âŸ¨m, hm1, hm2âŸ© := h1 n
      use âŸ¨f m, by simpâŸ©
      simp; rw [â†hn]
      have lt := f.monotone' hm1
      exact lt_of_le_of_ne lt hm2
    choose g hg using h2
    let g' : â„• â†’ Set.range f := Nat.rec âŸ¨(f 0), by simpâŸ© fun _ â†¦ g
    simp [Order.NoInfiniteChains] at h
    specialize h (Set.range f)
    have finf : IsChainLE (Set.range f) := by
      simp [IsChainLE, IsChain, Pairwise]
      intro x âŸ¨n, xmemâŸ© y âŸ¨m, ymemâŸ© ne
      cases Nat.lt_trichotomy n m with
      | inl h =>
        apply le_of_lt at h
        apply f.monotone' at h
        rw [â†xmem, â†ymem]
        left; assumption
      | inr h => cases h with
      | inl h => subst h; rw [xmem] at ymem; contradiction
      | inr h =>
        apply le_of_lt at h
        apply f.monotone' at h
        rw [â†xmem, â†ymem]
        right; assumption
    apply h at finf
    have noMax : NoMaxOrder (Set.range f) := by
      constructor
      intro a
      use âŸ¨g a, by simpâŸ©, hg a
    have inff : Infinite (Set.range f) := NoMaxOrder.infinite
    exact not_finite â†‘(Set.range â‡‘f)
  Â· intro f
    by_contra h1
    push_neg at h1
    let h2 : âˆ€ p : Set.range f, âˆƒ q, p < q := by
      intro âŸ¨p, âŸ¨n, hnâŸ©âŸ©
      obtain âŸ¨m, hm1, hm2âŸ© := h1 n
      use âŸ¨f m, by simpâŸ©
      simp; rw [â†hn]
      have lt := f.monotone' hm1
      exact lt_of_le_of_ne lt hm2
    choose g hg using h2
    let g' : â„• â†’ Set.range f := Nat.rec âŸ¨(f 0), by simpâŸ© fun _ â†¦ g
    apply Order.NoInfiniteChains.Dual at h
    simp [Order.NoInfiniteChains] at h
    specialize h (Set.range f)
    have finf : IsChainLE (Set.range f) := by
      simp [IsChainLE, IsChain, Pairwise]
      intro x âŸ¨n, xmemâŸ© y âŸ¨m, ymemâŸ© ne
      cases Nat.lt_trichotomy n m with
      | inl h =>
        apply le_of_lt at h
        apply f.monotone' at h
        rw [â†xmem, â†ymem]
        left; assumption
      | inr h => cases h with
      | inl h => subst h; rw [xmem] at ymem; contradiction
      | inr h =>
        apply le_of_lt at h
        apply f.monotone' at h
        rw [â†xmem, â†ymem]
        right; assumption
    apply h at finf
    have noMax : NoMaxOrder (Set.range f) := by
      constructor
      intro a
      use âŸ¨g a, by simpâŸ©, hg a
    have inff : Infinite (Set.range f) := NoMaxOrder.infinite
    exact not_finite â†‘(Set.range â‡‘f)


/-- What a beast to prove! I have a feeling there is a 2-3 liner available if I use
    use stuff from Mathlib. There may even be a way to shorten this argument. But
    this is the general structure of the proof in the book. -/
theorem example_2_40mpr [PartialOrder P] :
    Order.ACC P âˆ§ Order.DCC P â†’ Order.NoInfiniteChains P := by
  intro âŸ¨acc, dccâŸ©
  intro Q hQ
  by_contra infQ; simp [Finite] at infQ
  have neQ : Q.Nonempty := Set.Nonempty.of_subtype

  -- We proceed by finding a max xâ‚€ of Q, then a max xâ‚ of Q \ {x}, etc.
  -- The idea is that { q âˆˆ Q | xáµ¢ â‰¤ q } will be finite for each of these xáµ¢.
  -- Since { q âˆˆ Q | q < xáµ¢ } is the rest of Q, that will be infinite, allowing
  -- us to use choice to build a decreasing function on Q.

  -- First we show that for any x, Q = { q âˆˆ Q | q < x âˆ¨ x â‰¤ q }. Why is this so long?
  have hAll : âˆ€ x : Q, Q = { q âˆˆ Q | q < x } âˆª { q âˆˆ Q | x â‰¤ q } := by
    intro x
    ext z
    constructor
    Â· intro zmem
      simp [IsChainLE, IsChain] at hQ
      specialize hQ x.2 zmem
      by_cases eq : x = z
      Â· subst eq; simp
      Â· specialize hQ eq
        cases hQ with
        | inl hq => simp; right; use zmem, hq
        | inr hq =>
            simp; left; use zmem; obtain âŸ¨x, hxâŸ© := x; simp at eq âŠ¢; exact lt_of_le_of_ne hq fun a => eq (id (Eq.symm a))
    Â· intro zmem
      cases zmem with
      | inl zmem => exact zmem.1
      | inr zmem => exact zmem.1

  -- This is where we argue that if the upper set of the split is finite, the lower one must be infinite
  have key : âˆ€ x : { q âˆˆ Q | Finite { q' âˆˆ Q | q â‰¤ q' } }, Infinite { q âˆˆ Q | q < x } := by
    intro âŸ¨x, xmem, finXâŸ©
    rw [Set.infinite_coe_iff] at infQ âŠ¢
    rw [Set.finite_coe_iff] at finX
    by_contra finY
    rw [Set.not_infinite] at finY
    specialize hAll âŸ¨x, xmemâŸ©
    have finQ : ({ q âˆˆ Q | q < x} âˆª { q' âˆˆ Q | x â‰¤ q'}).Finite := Set.Finite.union finY finX
    rw [â†hAll] at finQ
    exact infQ finQ

  -- This is the heart of the proof. By showing that there is always a smaller element of Q, we can
  -- later use choice to build the decreasing function.
  have next : âˆ€ x : { q âˆˆ Q | Finite { q' âˆˆ Q | q â‰¤ q' } }, âˆƒ y, y < x := by
    intro x
    -- We want to take y to be the maximum of Iio x guranteed by 2_39. This step secretly uses
    -- Infinitude of { q âˆˆ Q | q < x } to infer it's nonempty. That is, it uses key.
    obtain âŸ¨y, âŸ¨âŸ¨ymem, ltâŸ©, ymaxâŸ©âŸ© := example_2_39.1 acc { q âˆˆ Q | q < x } Set.Nonempty.of_subtype
    -- This is the y we want. We are given y âˆˆ Q, and y < x. So it only remains to show that
    -- { q âˆˆ Q | y â‰¤ q } is finite.
    refine âŸ¨âŸ¨y, ymem, ?_âŸ©, ltâŸ©
    -- Since y was maximal below x, the elements above y are either y or also above x.
    have seq : { q âˆˆ Q | y â‰¤ q } = Set.insert y { q âˆˆ Q | x â‰¤ q } := by
      ext p
      constructor
      Â· intro pmem
        obtain âŸ¨pmem, lt'âŸ© := pmem
        simp [le_iff_lt_or_eq] at lt'
        cases lt' with
        | inl lt' =>
            simp [Set.insert]
            right
            simp [IsChainLE, IsChain] at hQ
            use pmem
            specialize hQ x.2.1 pmem
            by_cases eq : â†‘x = p
            Â· exact le_of_eq eq
            Â· specialize hQ eq
              cases hQ with
              | inl hQ => exact hQ
              | inr hQ =>
                  have ltpx : p < x := by exact lt_of_le_of_ne hQ fun a => eq (id (Eq.symm a))
                  specialize ymax âŸ¨pmem, ltpxâŸ© (le_of_lt lt')
                  exfalso
                  simp at lt' ymax
                  exact not_lt_of_le ymax lt'
        | inr eq => simp [Set.insert]; left; exact eq.symm
      Â· intro pmem
        simp [IsChainLE, IsChain] at hQ âŠ¢
        cases pmem with
        | inl eq => subst eq; use ymem
        | inr pmem =>
            use pmem.1
            exact (le_of_lt lt).trans pmem.2
    rw [seq]
    -- Since the set of elements above x is finite,
    have finX := x.2.2
    -- So is the set of elements above x together with y.
    apply Finite.Set.finite_insert
  -- We can thus build a decreasing function
  choose g hg using next
  -- To use the fact that decreasing functions are incompatible with DCC, we need an element
  -- of the set, which we can get by taking the max according to 2_39.
  have x := example_2_39.1 acc Q neQ
  obtain âŸ¨x, âŸ¨xmem, hxâŸ©âŸ© := x
  -- To prove x is in the set, we need to show the set of elements above it is finite.
  have finX : Finite { q âˆˆ Q | x â‰¤ q } := by
    have sing : { q âˆˆ Q | x â‰¤ q } = {x} := by
      ext a
      constructor
      Â· intro âŸ¨amem, ltâŸ©
        specialize hx amem lt
        apply eq_of_le_of_le hx at lt
        simpa
      Â· intro amem
        simp at amem âŠ¢
        subst amem
        use xmem
    rw [sing]
    exact Finite.of_subsingleton
  -- We thus get our contradiction
  apply Order.no_strict_dec_on_of_DCC dcc { q âˆˆ Q | Finite { q' âˆˆ Q | q â‰¤ q' } } g âŸ¨x, âŸ¨xmem, finXâŸ©âŸ© hg
